(import 'sys/func.inc)
(import 'sys/kernel/kernel.inc)
(import 'class/string/string.inc)
(import 'class/msg_out/msg_out.inc)
(import 'class/msg_in/msg_in.inc)
(import 'class/slave/slave.inc)
(import 'class/pipe/pipe.inc)

(def-func 'class/pipe/init)
	;inputs
	;r0 = pipe object (ptr)
	;r1 = vtable (pptr)
	;r2 = command buffer (pubyte)
	;r3 = command buffer length (bytes)
	;outputs
	;r0 = pipe object (ptr)
	;r1 = 0 if error, else ok
	;trashes
	;all but r0

	(ptr 'this 'vtable 'buffer)
	(ulong 'ok)
	(uint 'length)

	;read init args
	(push-scope)
	(f-entry 'pipe 'init {this, vtable, buffer, length})

	;init parent
	(s-call 'pipe 'init {this, vtable} {_, ok})
	(vpif {ok})
		;init state
		(f-call 'sys_mem 'clear {&this->pipe_start, pipe_size - pipe_start})
		(assign {stream_mail_state_started} {this->pipe_state})

		;init pipeline
		(ptr 'msg 'data 'string 'commands 'command 'args 'stream 'mbox 'ids 'cmd)
		(pubyte 'start)
		(ulong 'cpu)
		(struct 'mailbox 'mbox)
		(uint 'index 'started)

		(push-scope)
		;split pipe into separate commands and args
		(f-call 'stream 'create {0, 0, buffer, length} {stream})
		(f-call 'stream 'split {stream, char_pipe} {_, args})
		(f-call 'stream 'deref {stream})
		(d-call 'vector 'get_length {args} {_, length})
		(vpif {length})
			;create command prefix/suffix
			(f-call 'vector 'create {} {command})
			(f-call 'string 'create_from_cstr {"cmd/"} {cmd})
			(f-call 'vector 'push_back {command, cmd})
			(f-call 'string 'create_from_cstr {".lisp"} {cmd})
			(f-call 'string 'ref {cmd})
			(f-call 'vector 'push_back {command, cmd})
			(f-call 'vector 'push_back {command, cmd})

			;create command pipeline
			(f-call 'vector 'create {} {commands})
			(f-call 'vector 'set_capacity {commands, length})
			(assign {0} {index})
			(loop-while {index != length})
				(f-call 'vector 'get_element {args, index} {_, string})
				(f-call 'stream 'create {0, 0, &string->string_data, string->string_length} {stream})
				(f-call 'stream 'skip {stream, char_space})
				(assign {stream->stream_bufp} {start})
				(f-call 'stream 'skip_not {stream, char_space})
				(f-call 'string 'create_from_buffer {start, stream->stream_bufp - start} {string})
				(d-call 'vector 'set_element {command, string, 1})
				(f-call 'string 'cat {command} {cmd})
				(f-call 'vector 'push_back {commands, cmd})
				(f-call 'stream 'deref {stream})
				(assign {index + 1} {index})
			(loop-end)
			(f-call 'vector 'deref {command})

			;open command pipeline
			(f-call 'sys_task 'open_pipe {commands} {ids})
			(f-call 'vector 'deref {commands})

			;count how many started
			(assign {0, 0} {started, index})
			(loop-while {index != length})
				(f-call 'array 'get_element {ids, index * 2} {_, mbox})
				(vpif {mbox})
					(assign {started + 1} {started})
				(endif)
				(assign {index + 1} {index})
			(loop-end)

			;all started ?
			(vpif {started == length})
				;create streams, mailboxes and select array
				(f-call 'vector 'create {} {this->pipe_streams})
				(f-call 'vector 'set_capacity {this->pipe_streams, length + 2})
				(f-call 'sys_mem 'alloc {(length + 2) * ptr_size} {this->pipe_select_array, _})
				(assign {0} {index})
				(loop-while {index != (length + 2)})
					(f-call 'msg_in 'create {} {stream})
					(assign {stream->msg_in_mbox_addr} {mbox})
					(assign {mbox} {(this->pipe_select_array)[index * ptr_size]})
					(f-call 'vector 'push_back {this->pipe_streams, stream})
					(assign {index + 1} {index})
				(loop-end)

				;send args to pipe elements, wiring up id's as we go
				(f-call 'sys_mail 'init_mailbox {&mailbox})
				(f-call 'kernel 'id {} {cpu})
				(assign {index - 2} {index})
				(loop-while {index})
					(assign {index - 1} {index})
					(f-call 'vector 'get_element {args, index} {_, string})
					(f-call 'sys_mail 'alloc {slave_msg_init_size + string->string_length} {msg, data})
					(f-call 'sys_mem 'copy {&string->string_data, &data->slave_msg_init_args, string->string_length})
					(assign {mbox} {data->slave_msg_init_stdout_id.id_mbox})
					(assign {cpu} {data->slave_msg_init_stdout_id.id_cpu})
					(assign {(this->pipe_select_array)[(index + 1) * ptr_size]} {data->slave_msg_init_stderr_id.id_mbox})
					(f-call 'kernel 'id {} {data->slave_msg_init_stderr_id.id_cpu})
					(assign {&mailbox} {data->slave_msg_init_ack_id.id_mbox})
					(f-call 'kernel 'id {} {data->slave_msg_init_ack_id.id_cpu})
					(f-call 'array 'get_element {ids, index * 2} {_, mbox})
					(f-call 'array 'get_element {ids, index * 2 + 1} {_, cpu})
					(assign {mbox} {msg->msg_dest.id_mbox})
					(assign {cpu} {msg->msg_dest.id_cpu})
					(f-call 'sys_mail 'send {msg})

					;wait for ack with stdin mailbox
					(f-call 'sys_mail 'read {&mailbox} {msg, data})
					(assign {data->slave_msg_init_stdin_mbox} {mbox})
					(f-call 'sys_mail 'free {msg})
				(loop-end)

				;create input stream, free the unused space keeper stream
				(f-call 'msg_out 'create {mbox, cpu} {stream})
				(d-call 'vector 'set_element {this->pipe_streams, stream, 0})
			(else)
				(assign {0} {ok})
				;send abort to any started pipe elements
				(loop-start)
					(d-call 'array 'get_length {ids} {_, length})
					(breakifnot {length})
					(f-call 'array 'get_back {ids} {_, cpu})
					(d-call 'array 'pop_back {ids})
					(f-call 'array 'get_back {ids} {_, mbox})
					(d-call 'array 'pop_back {ids})
					(continueifnot {mbox})
					(f-call 'sys_mail 'alloc {0} {msg, _})
					(assign {mbox} {msg->msg_dest.id_mbox})
					(assign {cpu} {msg->msg_dest.id_cpu})
					(f-call 'sys_mail 'send {msg})
				(loop-end)
			(endif)

			;free ids
			(f-call 'array 'deref {ids})
		(endif)

		;free args
		(f-call 'vector 'deref {args})
		(pop-scope)
	(endif)

	(f-exit 'pipe 'init {this, ok})
	(pop-scope)
	(return)

(def-func-end)
