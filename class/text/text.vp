(import 'inc/func.inc)
(import 'inc/gui.inc)
(import 'inc/font.inc)
(import 'class/class_vector.inc)
(import 'class/class_text.inc)
(import 'class/class_string.inc)

(class-macro-new 'text)
(class-macro-create 'text)

(def-func 'class/text/init)
	;inputs
	;r0 = text object
	;r1 = vtable pointer
	;outputs
	;r1 = 0 if error, else ok

	;init parent
	(s-call 'text 'init '(r0 r1) '(r1))
	(vpif '(r1 != 0))
		(vp-push r0)

		;init myself
		(f-call 'gui_font 'open '("fonts/OpenSans-Regular.ttf" 18) '(r0))
		(assert '(r0 != 0))
		(vp-cpy-ir r4 0 r1)
		(vp-cpy-ri r0 r1 text_font)
		(vp-xor-rr r0 r0)
		(vp-cpy-ri r0 r1 text_string)
		(vp-cpy-ri r0 r1 text_text_color)
		(vp-cpy-ri r0 r1 text_words)

		(vp-pop r0)
	(endif)
	(vp-ret)

(def-func-end)

(def-func 'class/text/deinit)
	;inputs
	;r0 = text object
	;trashes
	;all but r0, r4

	;save object
	(vp-push r0)

	;deref any string and words objects
	(vp-cpy-ir r0 text_string r0)
	(vpif '(r0 != 0))
		(f-call 'string 'deref '(r0))
		(vp-cpy-ir r4 0 r0)
		(f-call 'vector 'deref '((r0 text_words)))
	(endif)

	;deinit parent
	(vp-pop r0)
	(s-jmp 'text 'deinit '(r0))

(def-func-end)

(def-func 'class/text/draw)
	;inputs
	;r0 = view object
	;r1 = ctx object
	;trashes
	;all but r0, r4

	(ptr 'inst 'ctx 'txt)
	(pptr 'words 'words_end)
	(ulong 'length)
	(long 'x)

	;save inputs
	(push-scope)
	(assign '(r0 r1) {inst, ctx})

	;draw text
	(vpif {inst->text_string && inst->text_font})
		(assign {inst->text_words->vector_array} {words})
		(devirt-call 'vector 'get_length {inst->text_words} {length})
		(assign {& words[length * ptr_size]} {words_end})
		(assign {0} {x})
		(loop-start)
			(func-call 'gui_font 'text {inst->text_font, * words} {txt})
			(vpif {txt})
				(func-call 'gui_ctx 'blit {ctx, txt->ft_text_texture, inst->text_text_color, x, 0, txt->ft_text_width, txt->ft_text_height})
				(assign {x + txt->ft_text_width + (txt->ft_text_height >> 2)} {x})
			(endif)
			(assign {words + ptr_size} {words})
		(loop-until {words == words_end})
	(endif)

	(assign {inst} '(r0))
	(pop-scope)
	(return)

(def-func-end)

(def-func 'class/text/get_text)
	;inputs
	;r0 = text object
	;outputs
	;r0 = text object
	;r1 = 0 else, string object
	;trashes
	;all but r0, r4

	(def-struct 'local)
		(ptr 'local_inst)
	(def-struct-end)

	;save inputs
	(vp-sub-cr local_size r4)
	(assign '(r0) '((r4 local_inst)))

	;reference the string
	(vp-cpy-ir r0 text_string r0)
	(vpif '(r0 != 0))
		(f-call 'string 'ref '(r0))
	(endif)
	(vp-cpy-rr r0 r1)

	(vp-cpy-ir r4 local_inst r0)
	(vp-add-cr local_size r4)
	(vp-ret)

(def-func-end)

(def-func 'class/text/pref_size)
	;inputs
	;r0 = text object
	;outputs
	;r10 = prefered width
	;r11 = prefered height
	;trashes
	;all but r0, r4

	(ptr 'inst)
	(pptr 'words 'words_end)
	(ulong 'width 'height 'length 'word_w)

	;save inputs
	(push-scope)
	(assign '(r0) {inst})

	;bounds of text
	(assign {0, 0} {width, height})
	(vpif {inst->text_string && inst->text_font})
		(assign {inst->text_words->vector_array} {words})
		(devirt-call 'vector 'get_length {inst->text_words} {length})
		(assign {& words[length * ptr_size]} {words_end})
		(loop-start)
			(func-call 'gui_font 'bounds {inst->text_font, * words} {word_w, height})
			(assign {words + ptr_size} {words})
			(assign {width + word_w} {width})
		(loop-until {words == words_end})
		(assign {width + (length - 1) * (height >> 2)} {width})
	(endif)

	(assign {inst, width, height} '(r0 r10 r11))
	(pop-scope)
	(return)

(def-func-end)

(def-func 'class/text/set_font)
	;inputs
	;r0 = text object
	;r1 = font name
	;r2 = point size
	;trashes
	;all but r0, r4

	(def-struct 'local)
		(ptr 'local_inst)
	(def-struct-end)

	;save inputs
	(vp-sub-cr local_size r4)
	(assign '(r0) '((r4 local_inst)))

	;open font
	(f-call 'gui_font 'open '(r1 r2) '(r0))

	(vp-cpy-ir r4 local_inst r1)
	(vp-cpy-ri r0 r1 text_font)

	(vp-cpy-ir r4 local_inst r0)
	(vp-add-cr local_size r4)
	(vp-ret)

(def-func-end)

(def-func 'class/text/set_text)
	;inputs
	;r0 = text object
	;r1 = 0, else string object
	;trashes
	;all but r0, r4

	(def-struct 'local)
		(ptr 'local_inst)
		(long 'local_string)
	(def-struct-end)

	;save inputs
	(vp-sub-cr local_size r4)
	(assign '(r0 r1) '((r4 local_inst) (r4 local_string)))

	;deref the old string and words
	(vp-cpy-ir r0 text_string r0)
	(vpif '(r0 != 0))
		(f-call 'string 'deref '(r0))
		(vp-cpy-ir r4 local_inst r0)
		(vp-cpy-ir r0 text_words r1)
		(vp-xor-rr r2 r2)
		(vp-cpy-ri r2 r0 text_words)
		(f-call 'string 'deref '(r1))
	(endif)

	;transfer reference to new string
	(vp-cpy-ir r4 local_inst r0)
	(vp-cpy-ir r4 local_string r1)
	(vp-cpy-ri r1 r0 text_string)
	(vpif '(r1 != 0))
		(f-call 'string 'split '(r1 32) '(r1))
		(vp-cpy-ir r4 local_inst r0)
		(vp-cpy-ri r1 r0 text_words)
	(endif)

	(vp-add-cr local_size r4)
	(vp-ret)

(def-func-end)

(def-func 'class/text/set_text_color)
	;inputs
	;r0 = text object
	;r1 = color

	(vp-cpy-ri r1 r0 text_text_color)
	(vp-ret)

(def-func-end)
