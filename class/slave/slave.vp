(import 'sys/func.inc)
(import 'class/vector/vector.inc)
(import 'class/msg_out/msg_out.inc)
(import 'class/msg_in/msg_in.inc)
(import 'class/string/string.inc)
(import 'class/slave/slave.inc)

(gen-new 'slave)
(gen-create 'slave)
(gen-class 'slave)

(def-func 'class/slave/init)
	;inputs
	;r0 = slave object (ptr)
	;r1 = vtable (pptr)
	;outputs
	;r1 = 0 if error, else ok
	;trashes
	;all but r0

	(ptr 'this 'vtable 'stream 'msg 'data 'my_mbox_addr)
	(ulong 'ok)
	(uint 'my_mbox_id)

	;read init args
	(push-scope)
	(f-entry 'slave 'init {this, vtable})

	;init parent
	(s-call 'slave 'init {this, vtable} {_, ok})
	(vpif {ok})
		;init myself
		(f-call 'sys_task 'mailbox {} {my_mbox_id})
		(f-call 'sys_mail 'mbox_addr {my_mbox_id} {my_mbox_addr})
		(f-call 'sys_mail 'read {my_mbox_addr} {msg, data})
		(vpif {msg->msg_length})
			;create stdin, stdout, stderr
			(f-call 'msg_in 'create {} {this->slave_stdin})
			(f-call 'msg_out 'create {data->slave_msg_init_stdout_id.id_id} {this->slave_stdout})
			(f-call 'msg_out 'create {data->slave_msg_init_stderr_id.id_id} {this->slave_stderr})

			;create args
			(f-call 'stream 'create {0, 0, &data->slave_msg_init_args, msg->msg_length - slave_msg_init_size} {stream})
			(f-call 'stream 'split {stream, char_space} {_, this->slave_args})
			(f-call 'stream 'deref {stream})

			;send back ack with stdin mailbox
			(assign {data->slave_msg_init_ack_id.id_id} {msg->msg_dest.id_id})
			(assign {this->slave_stdin->msg_in_mbox_id} {data->slave_msg_init_stdin_mbox_id})
			(f-call 'sys_mail 'send {msg})
		(else)
			;abort
			(f-call 'sys_mail 'free {msg})
			(assign {0} {ok})
		(endif)
	(endif)

	(f-exit 'slave 'init {this, ok})
	(pop-scope)
	(return)

(def-func-end)

(def-func 'class/slave/deinit)
	;inputs
	;r0 = slave object (ptr)
	;trashes
	;all but r0

	(ptr 'this)

	(push-scope)
	(f-entry 'slave 'deinit {this})

	;flush remaining
	(v-call 'msg_out 'write_flush {this->slave_stderr})
	(v-call 'msg_out 'write_flush {this->slave_stdout})

	;send stopping on stdout
	(f-call 'msg_out 'set_state {this->slave_stdout, stream_mail_state_stopping})
	(v-call 'msg_out 'write_next {this->slave_stdout})
	(v-call 'msg_out 'write_flush {this->slave_stdout})

	;wait for stopped
	(loop-while {this->slave_stdin->msg_in_state != stream_mail_state_stopped})
		(f-call 'msg_in 'next_msg {this->slave_stdin})
	(loop-end)

	;send stopped on stdout and stderr
	(f-call 'msg_out 'set_state {this->slave_stdout, stream_mail_state_stopped})
	(f-call 'msg_out 'set_state {this->slave_stderr, stream_mail_state_stopped})
	(v-call 'msg_out 'write_next {this->slave_stdout})
	(v-call 'msg_out 'write_flush {this->slave_stdout})
	(v-call 'msg_out 'write_next {this->slave_stderr})
	(v-call 'msg_out 'write_flush {this->slave_stderr})

	;free stdin, stdout and stderr
	(f-call 'msg_in 'deref {this->slave_stdin})
	(f-call 'msg_out 'deref {this->slave_stdout})
	(f-call 'msg_out 'deref {this->slave_stderr})

	;free args
	(f-call 'vector 'deref {this->slave_args})

	;deinit parent
	(f-exit 'slave 'deinit {this})
	(pop-scope)
	(s-jmp 'slave 'deinit '(r0))

(def-func-end)
