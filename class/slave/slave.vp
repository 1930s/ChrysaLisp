(import 'inc/func.inc)
(import 'class/vector/vector.inc)
(import 'class/stream_msg_out/stream_msg_out.inc)
(import 'class/stream_msg_in/stream_msg_in.inc)
(import 'class/string/string.inc)
(import 'class/slave/slave.inc)

(gen-new 'slave)
(gen-create 'slave)

(def-func 'class/slave/init)
	;inputs
	;r0 = slave object
	;r1 = vtable pointer
	;outputs
	;r1 = 0 if error, else ok
	;trashes
	;all but r0, r4

	(ptr 'inst 'vtable 'stream 'msg 'mymailbox)
	(ulong 'error)

	;read init args
	(push-scope)
	(assign '(r0 r1) {inst, vtable})

	;init parent
	(s-call 'slave 'init {inst, vtable} {error})
	(vpif {error != 0})
		;init myself
		(f-call 'sys_task 'mailbox {} {mymailbox, _})
		(f-call 'sys_mail 'read {mymailbox} {msg})
		(vpif {msg->msg_length != msg_header_size})
			;create stdin, stdout, stderr
			(f-call 'stream_msg_in 'create {mymailbox} {inst->slave_stdin})
			(f-call 'stream_msg_out 'create {msg->slave_mail_init_stdout_id.id_mbox, msg->slave_mail_init_stdout_id.id_cpu} {inst->slave_stdout})
			(f-call 'stream_msg_out 'create {msg->slave_mail_init_stderr_id.id_mbox, msg->slave_mail_init_stderr_id.id_cpu} {inst->slave_stderr})

			;create args
			(f-call 'stream 'create {0, 0, &msg->slave_mail_init_args, msg->msg_length - slave_mail_init_size} {stream})
			(f-call 'stream 'split {stream, char_space} {inst->slave_args})
			(f-call 'stream 'deref {stream})

			;send back ack
			(assign {msg->slave_mail_init_ack_id.id_mbox} {msg->msg_dest.id_mbox})
			(assign {msg->slave_mail_init_ack_id.id_cpu} {msg->msg_dest.id_cpu})
			(assign {msg_header_size} {msg->msg_length})
			(f-call 'sys_mail 'send {msg})
		(else)
			;abort
			(f-call 'sys_mem 'free {msg})
			(assign {0} {error})
		(endif)
	(endif)

	(assign {inst, error} '(r0 r1))
	(pop-scope)
	(return)

(def-func-end)

(def-func 'class/slave/deinit)
	;inputs
	;r0 = slave object
	;trashes
	;all but r0, r4

	(ptr 'inst 'msg)

	(push-scope)
	(assign '(r0) {inst})

	;flush remaining
	(v-call 'stream_msg_out 'write_flush {inst->slave_stderr})
	(v-call 'stream_msg_out 'write_flush {inst->slave_stdout})

	;send stopping on stdout
	(f-call 'stream_msg_out 'set_state {inst->slave_stdout, stream_mail_state_stopping})
	(v-call 'stream_msg_out 'write_next {inst->slave_stdout})
	(v-call 'stream_msg_out 'write_flush {inst->slave_stdout})

	;wait for stopped
	(loop-start)
		(v-call 'stream_msg_in 'read_next {inst->slave_stdin} {_})
	(loop-until {inst->slave_stdin->stream_msg_in_state == stream_mail_state_stopped})

	;send stopped on stdout and stderr
	(f-call 'stream_msg_out 'set_state {inst->slave_stdout, stream_mail_state_stopped})
	(f-call 'stream_msg_out 'set_state {inst->slave_stderr, stream_mail_state_stopped})
	(v-call 'stream_msg_out 'write_next {inst->slave_stdout})
	(v-call 'stream_msg_out 'write_flush {inst->slave_stdout})
	(v-call 'stream_msg_out 'write_next {inst->slave_stderr})
	(v-call 'stream_msg_out 'write_flush {inst->slave_stderr})

	;free stdin, stdout and stderr
	(f-call 'stream_msg_in 'deref {inst->slave_stdin})
	(f-call 'stream_msg_out 'deref {inst->slave_stdout})
	(f-call 'stream_msg_out 'deref {inst->slave_stderr})

	;free args
	(f-call 'vector 'deref {inst->slave_args})

	(assign {inst} '(r0))
	(pop-scope)

	;deinit parent
	(s-jmp 'slave 'deinit '(r0))

(def-func-end)
