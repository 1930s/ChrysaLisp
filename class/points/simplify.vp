(import 'sys/func.inc)
(import 'class/points/points.inc)
(import 'sys/math/math.inc)

(def-func 'class/points/simplify)
	;inputs
	;r0 = points object
	;r1 = source points object
	;r2 = stack array
	;r3 = 16.16 tolerance
	;outputs
	;r0 = points object
	;trashes
	;all but r0

	(ptr 'this 'that 'stack)
	(union
		'(ulong 'span 'rspan)
		'(uint 'first 'last 'index 'rlast))
	(ulong 'p1 'p2 'p3)
	(uint 'start 'length)
	(int 'eps 'max 'dist)

	(push-scope)
	(f-entry 'points 'simplify {this, that, stack, eps})

	;any point doing anything ?
	(d-call 'array 'get_length {that} {_, length})
	(vpif '{length <= 2})
		;just append to output
		(d-call 'array 'append {this, that, 0, length})
	(else)
		;first span
		(assign {0, length - 1} {first, last})

		;output first point
		(f-call 'array 'get_first {that} {_, p1})
		(f-call 'points 'push_back {this, p1})

		(d-call 'array 'get_length {stack} {_, start})
		(loop-start)
			;find max distance point
			(assign {eps, first + 1} {max, length})
			(f-call 'array 'get_element {that, first} {_, p2})
			(f-call 'array 'get_element {that, last} {_, p3})
			(loop-while {length < last})
				(f-call 'array 'get_element {that, length} {_, p1})
				(f-call 'math 'distance_sqd {p1, p2, p3} {dist})
				(vpif {dist > max})
					(assign {dist, length} {max, index})
				(endif)
				(assign {length + 1} {length})
			(loop-end)

			;continue subdivision ?
			(vpif {max > eps})
				(vpif {last - index > 1})
					(assign {last} {rlast})
					(f-call 'array 'push_back {stack, rspan})
				(endif)
				(vpif {index - first > 1})
					(assign {index} {first})
					(f-call 'array 'push_back {stack, span})
				(endif)
			(else)
				;output point
				(f-call 'array 'get_element {that, last} {_, p1})
				(f-call 'array 'push_back {this, p1})
			(endif)

			;next span
			(d-call 'array 'get_length {stack} {_, length})
			(breakif {length == start})
			(f-call 'array 'get_back {stack} {_, span})
			(d-call 'array 'pop_back {stack})
		(loop-end)
	(endif)

	(f-exit 'points 'simplify {this})
	(pop-scope)
	(return)

(def-func-end)
