(import 'sys/func.inc)
(import 'class/points/points.inc)
(import 'class/vector/vector.inc)
(import 'sys/math/math.inc)

(gen-new 'points)
(gen-create 'points)
(gen-class 'points)

(def-func 'class/points/rem_co_polyline)
	;inputs
	;r0 = points object
	;r1 = source points object, can be same
	;r2 = 16.16 tolerance
	;outputs
	;r0 = points object
	;trashes
	;all but r0, r4

	(ptr 'this 'that)
	(pptr 'iter_begin 'iter_end 'iter_this)
	(int 'eps)

	(push-scope)
	(f-entry 'points 'rem_co_polyline {this, that, eps})

	(f-call 'points 'get_begin {that} {_, iter_begin})
	(f-call 'points 'get_end {that} {_, iter_end})
	(f-call 'points 'get_begin {this} {_, iter_this})

	(vec-set 0)
	(vec-load 2 {iter_end, iter_begin})
	(vec-dup 2)
	(vec-dif 2)
	(vpif `(,(elem 0 (vec-pop 1)) > long_size))
		(vec-load 1 {eps})
		(vec-fsquare 1)
		(vec-load 1 {iter_this})
		(vec-def (vec-top 4) e i x j)
		(loop-start 'outer_loop)
			(vpif `(,i != ,j))
				(vec-load-long 1 i)
				(vec-store-long 1 j)
			(endif)
			(loop-start)
				(vp-add-cr long_size i)
				(breakif `(,i == ,e) outer_loop)
				(vec-load-int 2 j)
				(vec-load-int 2 i)
				(vec-fdistance-squared 2)
			(loop-until `(,(elem 0 (vec-pop 1)) >= ,x))
			(vp-add-cr long_size j)
		(loop-end)
		(vec-store 1 {iter_end})
		(f-call 'points 'set_length {this, (iter_end - iter_this >> 3) + 1})
	(endif)

	(f-exit 'points 'rem_co_polyline {this})
	(pop-scope)
	(return)

(def-func-end)

(def-func 'class/points/rem_co_polygon)
	;inputs
	;r0 = points object
	;r1 = source points object, can be same
	;r2 = 16.16 tolerance
	;outputs
	;r0 = points object
	;trashes
	;all but r0, r4

	(ptr 'this 'that)
	(pptr 'iter_begin 'iter_end)
	(int 'eps)

	(push-scope)
	(f-entry 'points 'rem_co_polygon {this, that, eps})

	(f-call 'points 'rem_co_polyline {this, that, eps})
	(f-call 'points 'get_begin {this} {_, iter_begin})
	(f-call 'points 'get_end {this} {_, iter_end})

	(vec-set 0)
	(vec-load 2 {iter_end, iter_begin})
	(vec-dup 2)
	(vec-dif 2)
	(vpif `(,(elem 0 (vec-pop 1)) > long_size))
		(vec-load 1 {eps})
		(vec-fsquare 1)
		(vec-def (vec-top 3) e i x)
		(vec-load-int 2 i)
		(vec-load-int 2 e (neg long_size))
		(vec-fdistance-squared 2)
		(vpif `(,(elem 0 (vec-pop 1)) < ,x))
			(d-call 'points 'pop_back {this})
		(endif)
	(endif)

	(f-exit 'points 'rem_co_polygon {this})
	(pop-scope)
	(return)

(def-func-end)

(def-func 'class/points/transform)
	;inputs
	;r0 = points object
	;r1 = source points object, can be same
	;r2 = 16.16/16.16 m1xy
	;r3 = 16.16/16.16 m2xy
	;r5 = 16.16/16.16 trxy
	;outputs
	;r0 = points object
	;trashes
	;all but r0, r4

	(ptr 'this 'that)
	(pptr 'iter_begin 'iter_end 'iter_this)
	(union
		'(ulong 'm1 'm2 'tr)
		'(int 'm1x 'm1y 'm2x 'm2y 'trx 'try))

	(push-scope)
	(f-entry 'points 'transform {this, that, m1, m2, tr})

	(f-call 'points 'get_begin {that} {_, iter_begin})
	(f-call 'points 'get_end {that} {_, iter_end})
	(f-call 'points 'get_begin {this} {_, iter_this})

	(vec-set 0)
	(vec-load 3 {iter_end, iter_begin, iter_this})
	(vec-def (vec-top 3) e i j)
	(vec-load 2 {trx, try})
	(defq vtr (vec-top 2))
	(loop-while `(,i != ,e))
		(vec-load-int 2 i)
		(defq vxy (vec-top 2))
		(vec-load 2 {m1x, m1y})
		(vec-fdot 2 vxy)
		(vec-load 2 {m2x, m2y})
		(vec-fdot 2 vxy)
		(vec-add 2 vtr)
		(vec-store-int 2 j)
		(vp-add-cr long_size i)
		(vp-add-cr long_size j)
	(loop-end)

	(f-exit 'points 'transform {this})
	(pop-scope)
	(return)

(def-func-end)
