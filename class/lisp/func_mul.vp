(import 'inc/func.inc)
(import 'class/error/error.inc)
(import 'class/vector/vector.inc)
(import 'class/boxed_long/boxed_long.inc)
(import 'class/lisp/lisp.inc)

(def-func 'class/lisp/func_mul)
	;inputs
	;r0 = lisp object
	;r1 = args
	;outputs
	;r0 = lisp object
	;r1 = value

	(ptr 'this 'args 'value)
	(pptr 'iter_begin 'iter_end)
	(union '(long 'accum 'num) '(uint 'length))

	(push-scope)
	(f-entry 'lisp 'func_mul {this, args})

	(d-call 'vector 'get_length {args} {_, length})
	(errorif {length < 2} 'error1)
	(f-call 'vector 'get_begin {args} {_, iter_begin})
	(f-call 'vector 'get_end {args} {_, iter_end})
	(assign {1} {accum})
	(loop-start)
		(assign {*iter_begin} {value})
		(errorif (cat {value->obj_vtable != @} (f-path 'class 'boxed_long)) 'error2)
		(f-call 'boxed_long 'get_value {value} {_, num})
		(assign {accum * num} {accum})
		(assign {iter_begin + ptr_size} {iter_begin})
	(loop-until {iter_begin == iter_end})
	(f-call 'boxed_long 'create {} {value})
	(f-call 'boxed_long 'set_value {value, accum})

(vp-label 'exit)
	(f-exit 'lisp 'func_mul {this, value})
	(return)

(errorcases
(vp-label 'error1)
	(f-call 'lisp 'repl_error {this, "(mul num num ...)", error_msg_wrong_num_of_args, args} {_, value})
	(goto 'exit)
(vp-label 'error2)
	(f-call 'lisp 'repl_error {this, "(mul num num ...)", error_msg_not_all_nums, args} {_, value})
	(goto 'exit))

	(pop-scope-syms)

(def-func-end)
