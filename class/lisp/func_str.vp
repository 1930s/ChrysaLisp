(import 'inc/func.inc)
(import 'class/class_vector.inc)
(import 'class/class_string.inc)
(import 'class/class_stream_str.inc)
(import 'class/class_error.inc)
(import 'class/class_lisp.inc)

(def-func 'class/lisp/func_str)
	;inputs
	;r0 = lisp object
	;r1 = args
	;outputs
	;r0 = lisp object
	;r1 = value

	(ptr 'this 'args 'value 'stream)
	(ulong 'length)

	(push-scope)
	(retire '(r0 r1) {this, args})

	(devirt-call 'vector 'get_length {args} {length})
	(vpif {length == 1})
		(func-call 'vector 'get_element {args, 0} {args})
		(vpif {args -> obj_vtable == @class/class_string})
			(assign {args} {value})
			(func-call 'ref 'ref {value})
		(elseif {args -> obj_vtable == @class/class_stream_str})
			(func-call 'stream_str 'ref_string {args} {value})
		(else)
			(func-call 'string 'create_from_cstr {"                "} {value})
			(func-call 'stream_str 'create {value} {stream})
			(func-call 'lisp 'repl_print {this, stream, args})
			(func-call 'stream_str 'ref_string {stream} {value})
			(func-call 'ref 'deref {stream})
		(endif)
	(else)
		(func-call 'error 'create {"(str arg) wrong number of args", args} {value})
	(endif)

	(expr {this, value} '(r0 r1))
	(pop-scope)
	(return)

(def-func-end)
