(import 'sys/func.inc)
(import 'sys/task/task.inc)
(import 'class/hash_set/hash_set.inc)
(import 'class/hash_map/hash_map.inc)
(import 'class/stream_str/stream_str.inc)
(import 'class/boxed_long/boxed_long.inc)
(import 'class/symbol/symbol.inc)
(import 'class/lisp/lisp.inc)

(def-struct 'built_in)
	(ushort 'symbol)
	(ushort 'field)
	(ushort 'function)
	(ushort 'flags)
	(offset 'hop)
	;built_in_size would be aligned to long !
(def-struct-end)

(defun built-in (s f &optional u g)
	;s = symbol
	;f = field
	;u = function
	;g = flags
	(vp-short
		(list 'sub (label-sym (string-sym (fn-add-string s))) '*pc*)
		f
		(if u (list 'sub (label-sym (link-sym (fn-find-link (f-path 'lisp u)))) '*pc* 4) 0)
		(if g g func_type_args_eval_apply)))

(def-func 'class/lisp/init)
	;inputs
	;r0 = lisp object object (ptr)
	;r1 = vtable (pptr)
	;r2 = stdin stream object (ptr)
	;r3 = stdout stream object (ptr)
	;r4 = stderr stream object (ptr)
	;outputs
	;r0 = lisp object object (ptr)
	;r1 = 0 if error, else ok
	;trashes
	;all

	(ptr 'this)
	(union
		'(ptr 'vtable 'stdin 'stdout 'stderr 'symbol 'table 'func)
		'(ptr 'repl_args 'value 'file 'stream))
	(ulong 'ok)

	(push-scope)
	(f-entry 'lisp 'init {this, vtable, stdin, stdout, stderr})

	;init parent
	(s-call 'lisp 'init {this, vtable} {_, ok})
	(vpif {ok})
		;init myself
		(f-call 'obj 'ref {stdin} {this->lisp_stdin})
		(f-call 'obj 'ref {stdout} {this->lisp_stdout})
		(f-call 'obj 'ref {stderr} {this->lisp_stderr})
		(assign {0} {this->lisp_nextsym})

		;spin lock if boot env busy
		(loop-while {ptr($boot_env) == -1})
			(f-call 'sys_task 'sleep {100})
		(loop-end)

		;do we have a shared boot env yet ?
		(vpifnot {ptr($boot_env)})
			;mark as busy
			(assign {-1} {ptr($boot_env)})

			;intern standard built in symbols
			;fixup built in functions
			(f-call 'hash_map 'create {0, 101} {this->lisp_environment})
			(assign {$built_ins} {table})
			(loop-while {table->built_in_symbol})
				(f-call 'symbol 'intern_cstr {&table->built_in_symbol + table->built_in_symbol} {symbol})
				(vpif {table->built_in_field})
					(assign {symbol} {ptr(this + table->built_in_field)})
				(endif)
				(vpif {table->built_in_function})
					(f-call 'boxed_ptr 'create {ptr(&table->built_in_function + table->built_in_function)
						| table->built_in_flags} {func})
					(f-call 'hash_map 'insert {this->lisp_environment, symbol, func})
					(f-call 'obj 'deref {func})
				(endif)
				(f-call 'obj 'deref {symbol})
				(assign {table + built_in_hop} {table})
			(loop-end)

			;define *stream-name* and *stream-line*
			(f-call 'string 'create_from_cstr {"ChrysaLisp"} {symbol})
			(f-call 'hash_map 'insert {this->lisp_environment, this->lisp_sym_stream_name, symbol})
			(f-call 'obj 'deref {symbol})
			(f-call 'boxed_long 'create {0} {symbol})
			(f-call 'hash_map 'insert {this->lisp_environment, this->lisp_sym_stream_line, symbol})
			(f-call 'obj 'deref {symbol})

			;run any boot.lisp
			(f-call 'string 'create_from_file {"class/lisp/boot.lisp"} {file})
			(vpif {file})
				;REPL from file stream
				(f-call 'vector 'create {} {repl_args})
				(f-call 'stream_str 'create {file} {stream})
				(f-call 'string 'create_from_cstr {"class/lisp/boot.lisp"} {file})
				(f-call 'vector 'push_back {repl_args, stream})
				(f-call 'vector 'push_back {repl_args, file})
				(f-call 'lisp 'func_repl {this, repl_args} {_, value})
				(vpif {value != this->lisp_sym_nil})
					(f-call 'lisp 'repl_print {this, this->lisp_stderr, value})
					(f-call 'stream 'write_char {this->lisp_stderr, char_lf})
					(v-call 'stream 'write_flush {this->lisp_stderr})
					(assign {0} {ok})
				(endif)
				(f-call 'obj 'deref {value})
				(f-call 'obj 'deref {repl_args})
			(endif)

			(vpifnot {ok})
				;deinit myself and parent
				(v-call 'lisp 'deinit {this})
				;mark as free
				(assign {0} {ptr($boot_env)})
			(else)
				;save shared boot env
				(f-call 'obj 'ref {this->lisp_environment} {ptr($boot_env)})
				(f-call 'hash_map 'create {this->lisp_environment->hash_set_key_callback, 101} {func})
				(f-call 'hash_map 'set_parent {func, this->lisp_environment} {this->lisp_environment})
			(endif)
		(else)
			;boot env is my parent env
			(f-call 'obj 'ref {ptr($boot_env)} {this->lisp_environment})
			(f-call 'hash_map 'create {this->lisp_environment->hash_set_key_callback, 101} {func})
			(f-call 'hash_map 'set_parent {func, this->lisp_environment} {this->lisp_environment})

			;look up standard built in symbols
			(assign {$built_ins} {table})
			(loop-while {table->built_in_symbol})
				(f-call 'symbol 'intern_cstr {&table->built_in_symbol + table->built_in_symbol} {symbol})
				(vpif {table->built_in_field})
					(assign {symbol} {ptr(this + table->built_in_field)})
				(endif)
				(f-call 'obj 'deref {symbol})
				(assign {table + built_in_hop} {table})
			(loop-end)

			;local define *stream-name* and *stream-line*
			(f-call 'string 'create_from_cstr {"ChrysaLisp"} {symbol})
			(f-call 'hash_map 'insert {this->lisp_environment, this->lisp_sym_stream_name, symbol})
			(f-call 'obj 'deref {symbol})
			(f-call 'boxed_long 'create {0} {symbol})
			(f-call 'hash_map 'insert {this->lisp_environment, this->lisp_sym_stream_line, symbol})
			(f-call 'obj 'deref {symbol})
		(endif)
	(endif)

	(f-exit 'lisp 'init {this, ok})
	(pop-scope)
	(return)

;;;;;;;;;;;;;;;;;;;;;;;
; shared boot eviroment
;;;;;;;;;;;;;;;;;;;;;;;

	(vp-align ptr_size)
(vp-label 'boot_env)
	(vp-long 0)

;;;;;;;;;;;
; built ins
;;;;;;;;;;;

	(vp-align short_size)
(vp-label 'built_ins)
	(built-in "_" lisp_sym_underscore)
	(built-in "&rest" lisp_sym_rest)
	(built-in "&optional" lisp_sym_optional)
	(built-in "nil" lisp_sym_nil)
	(built-in "t" lisp_sym_t)
	(built-in "list" lisp_sym_list)
	(built-in "lambda" lisp_sym_lambda)
	(built-in "macro" lisp_sym_macro)
	(built-in "unquote" lisp_sym_unquote)
	(built-in "unquote-splicing" lisp_sym_splicing)
	(built-in "quote" lisp_sym_quote)
	(built-in "quasi-quote" lisp_sym_qquote)
	(built-in "cat" lisp_sym_cat)
	(built-in "*stream-name*" lisp_sym_stream_name)
	(built-in "*stream-line*" lisp_sym_stream_line)
	(built-in "ffi" 0 'func_ffi func_type_args_apply)
	(vp-short 0)

(def-func-end)
