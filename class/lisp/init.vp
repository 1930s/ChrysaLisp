(import 'sys/func.inc)
(import 'class/hash_set/hash_set.inc)
(import 'class/hash_map/hash_map.inc)
(import 'class/stream_str/stream_str.inc)
(import 'class/boxed_long/boxed_long.inc)
(import 'class/symbol/symbol.inc)
(import 'class/lisp/lisp.inc)

(def-struct 'built_in)
	(ushort 'symbol)
	(ushort 'field)
	(ushort 'function)
	(ushort 'flags)
	(offset 'hop)
	;built_in_size would be aligned to long !
(def-struct-end)

(defun built-in (s f &optional u g)
	;s = symbol
	;f = field
	;u = function
	;g = flags
	(vp-short
		(list 'sub (label-sym (string-sym (fn-add-string s))) '*pc*)
		f
		(if u (list 'sub (label-sym (link-sym (fn-find-link (f-path 'lisp u)))) '*pc* 4) 0)
		(if g g func_type_args_eval_apply)))

(def-func 'class/lisp/init)
	;inputs
	;r0 = object
	;r1 = vtable pointer
	;r2 = stdin stream
	;r3 = stdout stream
	;r4 = stderr stream
	;outputs
	;r0 = object
	;r1 = 0 if error, else ok
	;trashes
	;all

	(ptr 'this)
	(union
		'(ptr 'vtable 'stdin 'stdout 'stderr 'symbol 'table 'func)
		'(ptr 'repl_args 'value 'file 'stream))
	(ulong 'ok)

	(push-scope)
	(f-entry 'lisp 'init {this, vtable, stdin, stdout, stderr})

	;init parent
	(s-call 'lisp 'init {this, vtable} {_, ok})
	(vpif {ok})
		;init myself
		(f-call 'ref 'ref {stdin} {this->lisp_stdin})
		(f-call 'ref 'ref {stdout} {this->lisp_stdout})
		(f-call 'ref 'ref {stderr} {this->lisp_stderr})
		(assign {0} {this->lisp_nextsym})
		(f-call 'hash_map 'create {0, 101} {this->lisp_environment})

		;intern standard built in symbols
		;fixup built in functions
		(assign {$built_ins} {table})
		(loop-while {table->built_in_symbol})
			(f-call 'symbol 'intern_cstr {&table->built_in_symbol + table->built_in_symbol} {symbol})
			(vpif {table->built_in_field})
				(assign {symbol} {ptr(this + table->built_in_field)})
			(endif)
			(vpif {table->built_in_function})
				(f-call 'boxed_ptr 'create {ptr(&table->built_in_function + table->built_in_function)
					| table->built_in_flags} {func})
				(f-call 'hash_map 'insert {this->lisp_environment, symbol, func})
				(f-call 'ref 'deref {func})
			(endif)
			(f-call 'ref 'deref {symbol})
			(assign {table + built_in_hop} {table})
		(loop-end)

		;define *stream-name* and *stream-line*
		(f-call 'string 'create_from_cstr {"ChrysaLisp"} {symbol})
		(f-call 'hash_map 'insert {this->lisp_environment, this->lisp_sym_stream_name, symbol})
		(f-call 'ref 'deref {symbol})
		(f-call 'boxed_long 'create {0} {symbol})
		(f-call 'hash_map 'insert {this->lisp_environment, this->lisp_sym_stream_line, symbol})
		(f-call 'ref 'deref {symbol})
	(endif)

	;run any boot.lisp
	(f-call 'string 'create_from_file {"class/lisp/boot.lisp"} {file})
	(vpif {file})
		;REPL from file stream
		(f-call 'vector 'create {} {repl_args})
		(f-call 'stream_str 'create {file} {stream})
		(f-call 'string 'create_from_cstr {"class/lisp/boot.lisp"} {file})
		(f-call 'vector 'push_back {repl_args, stream})
		(f-call 'vector 'push_back {repl_args, file})
		(f-call 'lisp 'func_repl {this, repl_args} {_, value})
		(vpif (cat {value->obj_vtable == @} (f-path 'class 'error)))
			(f-call 'lisp 'repl_print {this, this->lisp_stderr, value})
			(f-call 'stream 'write_char {this->lisp_stderr, char_lf})
			(v-call 'stream 'write_flush {this->lisp_stderr})
		(endif)
		(f-call 'ref 'deref {value})
		(f-call 'ref 'deref {repl_args})
	(endif)

	(f-exit 'lisp 'init {this, ok})
	(pop-scope)
	(return)

;;;;;;;;;;;
; built ins
;;;;;;;;;;;

	(vp-align short_size)
(vp-label 'built_ins)
	(built-in "_" lisp_sym_underscore)
	(built-in "&rest" lisp_sym_rest)
	(built-in "&optional" lisp_sym_optional)
	(built-in "nil" lisp_sym_nil)
	(built-in "t" lisp_sym_t)
	(built-in "list" lisp_sym_list)
	(built-in "lambda" lisp_sym_lambda)
	(built-in "macro" lisp_sym_macro)
	(built-in "unquote" lisp_sym_unquote)
	(built-in "unquote-splicing" lisp_sym_splicing)
	(built-in "quote" lisp_sym_quote)
	(built-in "quasi-quote" lisp_sym_qquote)
	(built-in "cat" lisp_sym_cat)
	(built-in "*stream-name*" lisp_sym_stream_name)
	(built-in "*stream-line*" lisp_sym_stream_line)
	(built-in "ffi" 0 'func_ffi func_type_args_apply)
	(vp-short 0)

(def-func-end)
