(import 'inc/func.inc)
(import 'inc/sdl2.inc)
(import 'class/text/text.inc)
(import 'class/canvas/canvas.inc)
(import 'class/flow/flow.inc)

(gen-new 'canvas)
(gen-class 'canvas)

(def-func 'class/canvas/create)
	;inputs
	;r0 = width
	;r1 = height
	;r2 = aa scale, 16.16
	;outputs
	;r0 = 0 if error, else object
	;trashes
	;r1-r3, r5-r7

	;save inputs
	(f-entry 'canvas 'create '(r5 r6 r7))

	;create new object
	(f-call 'canvas 'new nil '(r0))
	(vpif '(r0 != 0))
		;init the object
		(f-call 'canvas 'init `(r0 (@ ,(f-path 'class 'canvas)) r5 r6 r7) '(r0 r1))
		(vpif '(r1 == 0))
			;error with init
			(v-call 'canvas 'delete '(r0) '() 'r1)
			(vp-xor-rr r0 r0)
		(endif)
	(endif)

	(f-exit 'canvas 'create '(r0))
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/init)
	;inputs
	;r0 = canvas object
	;r1 = vtable pointer
	;r2 = width
	;r3 = height
	;r5 = aa scale, 16.16
	;outputs
	;r1 = 0 if error, else ok

	;save inputs
	(f-entry 'canvas 'init '(r0 r1 (r0 canvas_width i) (r0 canvas_height i) (r0 canvas_aa_scale i)))

	;init parent
	(s-call 'canvas 'init '(r0 r1) '(r0 r1))
	(vpif '(r1 != 0))
		(vp-push r0)
		(vp-xor-rr r2 r2)
		(vp-cpy-ri r2 r0 canvas_texture)
		(vp-cpy-ri r2 r0 canvas_resize_buf)
		(vp-cpy-ri-i r2 r0 canvas_resize_buf_size)
		(vp-cpy-ri-i r2 r0 canvas_cx)
		(vp-cpy-ri-i r2 r0 canvas_cy)

		(vp-cpy-ir-i r0 canvas_aa_scale r3)
		(vp-cpy-cr (bit-shl fp-one 16) r2)
		(vp-xor-rr r1 r1)
		(vp-div-rrr r3 r1 r2)
		(vp-cpy-ri-i r2 r0 canvas_aa_scale_r)
		(vp-cpy-ir-i r0 canvas_aa_scale r3)
		(vp-cpy-ir-ui r0 canvas_width r1)
		(vp-cpy-ir-ui r0 canvas_height r2)
		(vp-mul-rr r3 r1)
		(vp-mul-rr r3 r2)
		(vp-asr-cr 16 r1)
		(vp-asr-cr 16 r2)
		(vp-cpy-ri-i r1 r0 canvas_width)
		(vp-cpy-ri-i r1 r0 canvas_height)
		(vp-cpy-ri-i r1 r0 canvas_cx1)
		(vp-cpy-ri-i r2 r0 canvas_cy1)

		;pixel buffer
		(vp-shl-cr (pow2 int_size) r1)
		(vp-cpy-ri r1 r0 canvas_stride)
		(vp-mul-rr r2 r1)
		(f-call 'sys_mem 'alloc '(r1) '(r1 _))
		(vp-pop r0)
		(vp-cpy-ri r1 r0 canvas_data)
	(endif)

	(f-exit 'canvas 'init '(r0 r1))
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/deinit)
	;inputs
	;r0 = canvas object

	;kernel callback
	(vp-push r0)
	(f-call 'sys_task 'callback '(($ callback) r0))
	(vp-pop r0)
	(s-jmp 'canvas 'deinit '(r0))

(vp-label 'callback)
	;inputs
	;r0 = user data

	(def-struct 'local)
		(ptr 'this)
		(ptr 'old_stack)
	(def-struct-end)

	;align stack for SDL
	(vp-cpy-rr r4 r15)
	(vp-sub-cr local_size r4)
	(vp-and-cr -16 r4)
	(vp-cpy-ri r15 r4 local_old_stack)

	;save inputs
	(f-entry 'canvas 'deinit '((r4 local_this)))

	;free any pixel buffer
	(f-call 'sys_mem 'free '((r0 canvas_data)))

	;free any resize buffer
	(vp-cpy-ir r4 local_this r0)
	(f-call 'sys_mem 'free '((r0 canvas_resize_buf)))

	;free any sdl texture
	(vp-cpy-ir r4 local_this r0)
	(vp-cpy-ir r0 canvas_texture r1)
	(vpif '(r1 != 0))
		(sdl-destroy-texture '(r1))
	(endif)

	(vp-cpy-ir r4 local_old_stack r4)
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/draw)
	;inputs
	;r0 = canvas object
	;r1 = ctx object
	;trashes
	;all but r0, r4

	(def-struct 'local)
		(ptr 'this)
		(ptr 'ctx)
		(ptr 'surface)
		(ptr 'temp_canvas)
		(ptr 'old_stack)
	(def-struct-end)

	;align stack to 16 bytes for SDl
	(vp-cpy-rr r4 r15)
	(vp-sub-cr local_size r4)
	(vp-and-cr -16 r4)
	(vp-cpy-ri r15 r4 local_old_stack)

	;save inputs
	(f-entry 'canvas 'draw '((r4 local_this) (r4 local_ctx)))

	;if no texture then create from pixel buffer
	(vp-cpy-ir r0 canvas_texture r1)
	(vpif '(r1 == 0))
		;create SDL surface from pixel buffer, convert to texture
		(vp-cpy-ir-i r0 canvas_aa_scale_r r8)
		(vp-cpy-ir-ui r0 canvas_width r10)
		(vp-cpy-ir-ui r0 canvas_height r11)
		(vp-mul-rr r8 r10)
		(vp-mul-rr r8 r11)
		(vp-asr-cr 16 r10)
		(vp-asr-cr 16 r11)
		(f-call 'canvas 'create '(r10 r11 fp-one) '(r0))
		(vp-cpy-ri r0 r4 local_temp_canvas)
		(f-call 'canvas 'resize '(r0 (r4 local_this)))
		(sdl-create-rgb-surface-from '((r0 canvas_data) (r0 canvas_width ui) (r0 canvas_height ui)
										32 (r0 canvas_stride ui) 0xff 0xff00 0xff0000 0xff000000))
		(vp-cpy-ri r0 r4 local_surface)
		(vp-cpy-ir r4 local_ctx r1)
		(sdl-create-texture-from-surface '((r1 gui_ctx_sdl_ctx) r0))
		(vp-cpy-ir r4 local_this r1)
		(vp-cpy-ri r0 r1 canvas_texture)
		(sdl-free-surface '((r4 local_surface)))
		(f-call 'canvas 'deref '((r4 local_temp_canvas)))
	(endif)

	;blit texture
	(vp-cpy-ir r4 local_this r0)
	(vp-cpy-ir r0 canvas_texture r1)
	(vpif '(r1 != 0))
		(vp-cpy-ir-i r0 canvas_aa_scale_r r8)
		(vp-cpy-ir-ui r0 canvas_width r10)
		(vp-cpy-ir-ui r0 canvas_height r11)
		(vp-mul-rr r8 r10)
		(vp-mul-rr r8 r11)
		(vp-asr-cr 16 r10)
		(vp-asr-cr 16 r11)
		(f-call 'gui_ctx 'blit '((r4 local_ctx) r1 0xffffff 0 0 r10 r11))
	(endif)

	(f-exit 'canvas 'draw '((r4 local_this)))
	(vp-cpy-ir r4 local_old_stack r4)
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/swap)
	;inputs
	;r0 = canvas object
	;trashes
	;all but r0, r4

	;kernel callback
	(vp-push r0)
	(f-call 'sys_task 'callback '(($ callback) r0))
	(vp-pop r0)
	(vp-ret)

(vp-label 'callback)
	;inputs
	;r0 = user data

	(def-struct 'local)
		(ptr 'old_stack)
	(def-struct-end)

	;align stack to 16 bytes for SDl
	(vp-cpy-rr r4 r15)
	(vp-sub-cr local_size r4)
	(vp-and-cr -16 r4)
	(vp-cpy-ri r15 r4 local_old_stack)

	;free any sdl texture
	(vp-cpy-ir r0 canvas_texture r1)
	(vpif '(r1 != 0))
		(vp-xor-rr r2 r2)
		(vp-cpy-ri r2 r0 canvas_texture)
		(sdl-destroy-texture '(r1))
	(endif)

	(vp-cpy-ir r4 local_old_stack r4)
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/pref_size)
	;inputs
	;r0 = canvas object
	;outputs
	;r10 = prefered width
	;r11 = prefered height
	;trashes
	;all but r0, r4

	(f-entry 'canvas 'pref_size '(r0))

	(vp-cpy-ir-i r0 canvas_aa_scale_r r8)
	(vp-cpy-ir-ui r0 canvas_width r10)
	(vp-cpy-ir-ui r0 canvas_height r11)
	(vp-mul-rr r8 r10)
	(vp-mul-rr r8 r11)
	(vp-asr-cr 16 r10)
	(vp-asr-cr 16 r11)

	(f-exit 'canvas 'pref_size '(r0 r10 r11))
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/fill)
	;inputs
	;r0 = canvas object
	;r1 = colour
	;outputs
	;r0 = canvas object
	;trashes
	;r2-r3, r5

	(f-entry 'canvas 'fill '(r0 r1))

	(vp-cpy-ir r0 canvas_data r2)
	(vp-cpy-ir-ui r0 canvas_stride r3)
	(vp-cpy-ir-ui r0 canvas_height r5)
	(vp-mul-rr r5 r3)
	(vp-add-rr r2 r3)
	(loop-start)
		(vp-cpy-ri-i r1 r2 0)
		(vp-add-cr int_size r2)
	(loop-until '(r2 == r3))

	(f-exit 'canvas 'fill '(r0))
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/set_clip)
	;inputs
	;r0 = canvas object
	;r8 = x
	;r9 = y
	;r10 = x1
	;r11 = y1
	;outputs
	;r0 = canvas object
	;trashes
	;r1-r2

	(f-entry 'canvas 'set_clip '(r0 r8 r9 r10 r11))

	;clip check
	(switch)
	(breakif '(r8 > r10))
	(breakif '(r9 > r11))
	(vp-cpy-ir-ui r0 canvas_width r1)
	(breakif '(r8 >= r1))
	(vp-cpy-ir-ui r0 canvas_height r2)
	(breakif '(r9 >= r2))
	(breakif '(r10 < 0))
	(breakif '(r11 < 0))
	(vpif '(r8 < 0))
		(vp-xor-rr r8 r8)
	(endif)
	(vpif '(r9 < 0))
		(vp-xor-rr r9 r9)
	(endif)
	(vpif '(r10 > r1))
		(vp-cpy-rr r1 r10)
	(endif)
	(vpif '(r11 > r2))
		(vp-cpy-rr r2 r11)
	(endif)
	(vp-cpy-ri-i r8 r0 canvas_cx)
	(vp-cpy-ri-i r9 r0 canvas_cy)
	(vp-cpy-ri-i r10 r0 canvas_cx1)
	(vp-cpy-ri-i r11 r0 canvas_cy1)
	(endswitch)

	(f-exit 'canvas 'set_clip '(r0))
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/set_pixel)
	;inputs
	;r0 = canvas object
	;r1 = colour
	;r8 = x
	;r9 = y
	;outputs
	;r0 = canvas object
	;trashes
	;r2-r3, r8-r9

	(f-entry 'canvas 'set_pixel '(r0 r1 r8 r9))

	;clip check
	(switch)
	(vp-cpy-ir-ui r0 canvas_cx r2)
	(breakif '(r8 < r2))
	(vp-cpy-ir-ui r0 canvas_cy r2)
	(breakif '(r9 < r2))
	(vp-cpy-ir-ui r0 canvas_cx1 r2)
	(breakif '(r8 >= r2))
	(vp-cpy-ir-ui r0 canvas_cy1 r2)
	(breakif '(r9 >= r2))
		(vp-cpy-ir-ui r0 canvas_stride r2)
		(vp-cpy-ir r0 canvas_data r3)
		(vp-mul-rr r2 r9)
		(vp-shl-cr (pow2 int_size) r8)
		(vp-add-rr r9 r3)
		(vp-cpy-rd-i r1 r3 r8)
	(endswitch)

	(f-exit 'canvas 'set_pixel '(r0))
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/set_hline)
	;inputs
	;r0 = canvas object
	;r1 = colour
	;r8 = x
	;r9 = y
	;r10 = w
	;outputs
	;r0 = canvas object
	;trashes
	;r2-r3, r8-r10

	(f-entry 'canvas 'set_hline '(r0 r1 r8 r9 r10))

	;clip check
	(switch)
	(breakif '(r10 <= 0))
	(vp-cpy-ir-ui r0 canvas_cy r2)
	(breakif '(r9 < r2))
	(vp-cpy-ir-ui r0 canvas_cy1 r2)
	(breakif '(r9 >= r2))
	(vp-cpy-ir-ui r0 canvas_cx r2)
	(vp-add-rr r8 r10)
	(breakif '(r10 <= r2))
	(vp-cpy-ir-ui r0 canvas_cx1 r3)
	(breakif '(r8 >= r3))
		(vpif '(r8 < r2))
			(vp-cpy-rr r2 r8)
		(endif)
		(vpif '(r10 > r3))
			(vp-cpy-rr r3 r10)
		(endif)
		(vp-cpy-ir-ui r0 canvas_stride r2)
		(vp-cpy-ir r0 canvas_data r3)
		(vp-mul-rr r2 r9)
		(vp-shl-cr (pow2 int_size) r8)
		(vp-shl-cr (pow2 int_size) r10)
		(vp-add-rr r9 r3)
		(loop-start)
			(vp-cpy-rd-i r1 r3 r8)
			(vp-add-cr int_size r8)
		(loop-until '(r8 == r10))
	(endswitch)

	(f-exit 'canvas 'set_hline '(r0))
	(vp-ret)

(def-func-end)

(def-func 'class/canvas/resize)
	;inputs
	;r0 = canvas object
	;r1 = source canvas object
	;outputs
	;r0 = canvas object
	;trashes
	;all but r0, r4

	(ptr 'this 'source)
	(puint 'data 'dsrc)
	(uint 'x 'y 'x2 'y2)
	(uint 'x1a 'x1b 'x1c 'x1d 'y1a 'y1b 'y1c 'y1d)
	(uint 'a 'r 'g 'b 'q 'weight_x 'weight_y)
	(long 'fw 'fh)

	(push-scope)
	(f-entry 'canvas 'resize {this, source})

	(assign {(this->canvas_width + 1) * int_size} {x})
	(vpif {x > this->canvas_resize_buf_size})
		(vpif {this->canvas_resize_buf})
			(f-call 'sys_mem 'free {this->canvas_resize_buf})
		(endif)
		(f-call 'sys_mem 'alloc {x} {this->canvas_resize_buf, this->canvas_resize_buf_size})
	(endif)

	(assign {(source->canvas_width * 256) </ this->canvas_width} {fw})
	(assign {(source->canvas_height * 256) </ this->canvas_height} {fh})

	(assign {0} {x2})
	(loop-while {x2 < this->canvas_width})
		(assign {x2 *> fw} {(this->canvas_resize_buf)[x2 * int_size]})
		(assign {x2 + 1} {x2})
	(loop-end)
	(assign {source->canvas_width * 256} {(this->canvas_resize_buf)[x2 * int_size]})

	(assign {this->canvas_data} {data})
	(assign {0} {y2})
	(loop-while {y2 < this->canvas_height})
		(assign {y2 *> fh} {y1a})
		(assign {y1a >> 8} {y1c})
		(assign {((y2 + 1) *> fh) - 1} {y1b})
		(assign {y1b >> 8} {y1d})

		(assign {0} {x2})
		(loop-while {x2 < this->canvas_width})
			(assign {(this->canvas_resize_buf)[x2 * int_size]} {x1a})
			(assign {x1a >> 8} {x1c})
			(assign {(this->canvas_resize_buf)[(x2 + 1) * int_size] - 1} {x1b})
			(assign {x1b >> 8} {x1d})

			(assign {0, 0, 0, 0, 0} {a, r, g, b, q})

			(assign {y1c} {y})
			(loop-while {y < y1d})
				(assign {256} {weight_y})
				(vpif {y1c != y1d})
					(vpif {y == y1c})
						(assign {weight_y - (y1a & 0xFF)} {weight_y})
					(elseif {y == y1d})
						(assign {(y1b & 0xFF) + 1} {weight_y})
					(endif)
				(endif)

				(assign {&(source->canvas_data)[(y * source->canvas_width + x1c) * int_size]} {dsrc})
				(assign {x1c} {x})
				(loop-while {x < x1d})
					(assign {256} {weight_x})
					(vpif {x1c != x1d})
						(vpif {x == x1c})
							(assign {weight_x - (x1a & 0xFF)} {weight_x})
						(elseif {x == x1d})
							(assign {(x1b & 0xFF) + 1} {weight_x})
						(endif)
					(endif)

					;multiply pixel cahnnels by the pixel weight and accumalate
					(assign {*dsrc, weight_x * weight_y, r, g, b, a, q}
							'(r0 r5 r8 r9 r10 r11 r12))
					(vp-cpy-rr r0 r1)
					(vp-cpy-rr r0 r2)
					(vp-cpy-rr r0 r3)
					(vp-shr-cr 8 r1)
					(vp-shr-cr 16 r2)
					(vp-shr-cr 24 r3)
					(vp-and-cr 0xff r0)
					(vp-and-cr 0xff r1)
					(vp-and-cr 0xff r2)
					(vp-mul-rr r5 r0)
					(vp-mul-rr r5 r1)
					(vp-mul-rr r5 r2)
					(vp-mul-rr r5 r3)
					(vp-add-rr r8 r0)
					(vp-add-rr r9 r1)
					(vp-add-rr r10 r2)
					(vp-add-rr r11 r3)
					(vp-add-rr r5 r12)
					(assign '(r0 r1 r2 r3 r12) {r, g, b, a, q})

					(assign {dsrc + int_size} {dsrc})
					(assign {x + 1} {x})
				(loop-end)
				(assign {y + 1} {y})
			(loop-end)

			(assign {(r / q) | ((g / q) << 8) | ((b / q) << 16) | ((a / q) << 24)} {*data})
			(assign {data + int_size} {data})

			(assign {x2 + 1} {x2})
		(loop-end)
		(assign {y2 + 1} {y2})
	(loop-end)

	(f-exit 'canvas 'resize {this})
	(pop-scope)
	(return)

(def-func-end)
