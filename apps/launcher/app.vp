(import 'sys/func.inc)
(import 'sys/kernel/kernel.inc)
(import 'gui/gui.inc)
(import 'sys/string/string.inc)
(import 'class/window/window.inc)
(import 'class/flow/flow.inc)
(import 'class/button/button.inc)
(import 'class/string/string.inc)

(def-func 'apps/launcher/app)

	(ptr 'msg 'data 'window 'panel 'button 'string)
	(pubyte 'next)
	(int 'width 'height)
	(uint 'length)

	;init app vars
	(push-scope)

	;create my window
	(f-call 'window 'create {0} {window})
	(f-call 'window 'set_title {window, "Launcher"})

	;add my app panel
	(f-call 'flow 'create {} {panel})
	(f-call 'flow 'set_long_prop {panel, prop_flow_flags, flow_flag_down | flow_flag_fillw})
	(v-call 'window 'add_child {window, panel})

	;add launch buttons to my app panel
	(assign {$launch_list} {next})
	(loop-while {*next})
		(f-call 'button 'create {} {button})
		(f-call 'button 'set_long_prop {button, prop_color, 0xffffff00})
		(f-call 'button 'set_string_prop {button, prop_text, next})
		(v-call 'flow 'add_child {panel, button})
		(f-call 'button 'add_clicked {button, 0})
		(f-call 'sys_string 'length {next} {_, length})
		(assign {next + length + byte_size} {next})
	(loop-end)

	;set to pref size
	(v-call 'window 'pref_size {window} {_, width, height})
	(f-call 'window 'change {window, 32, 32, width + 40, height})

	;add to screen
	(f-call 'gui_gui 'add {window})

	;app event loop
	(loop-start)
		(f-call 'sys_mail 'mymail {} {msg, data})

		;dispatch event to target
		(vpif {data->ev_msg_target_id >= 0})
			(f-call 'window 'find_id {window, data->ev_msg_action_source_id} {_, button})
			(f-call 'button 'get_prop {button, prop_text} {_, string})
			(f-call 'sys_task 'open_child {string, kn_call_open})
		(else)
			(v-call 'view 'event {window, data})
		(endif)

		;free event message
		(f-call 'sys_mail 'free {msg})
	(loop-end)

	;deref window
	(f-call 'window 'deref {window})
	(pop-scope)
	(return)

(vp-label 'launch_list)
	(vp-string "apps/netmon/app") (vp-byte 0)
	(vp-string "apps/terminal/app") (vp-byte 0)
	(vp-string "apps/canvas/app") (vp-byte 0)
	(vp-string "apps/raymarch/app") (vp-byte 0)
	(vp-string "apps/calculator/app") (vp-byte 0)
	(vp-string "tests/farm") (vp-byte 0)
	(vp-string "tests/pipe") (vp-byte 0)
	(vp-string "tests/global") (vp-byte 0)
	(vp-string "tests/migrate") (vp-byte 0)
	(vp-byte 0)

(def-func-end)
