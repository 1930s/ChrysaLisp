(import 'sys/func.inc)
(import 'sys/mail/mail.inc)
(import 'gui/gui.inc)
(import 'sys/string/string.inc)
(import 'class/window/window.inc)
(import 'class/flow/flow.inc)
(import 'class/grid/grid.inc)
(import 'class/button/button.inc)
(import 'class/string/string.inc)

(def-func 'apps/calculator/app)

	(def-struct 'local 'obj)
		(ptr 'display)
		(long 'accum)
		(long 'value)
		(struct 'buffer 32)
		(uint 'last_op)
		(uint 'last_flag)
		(uint 'closed)
	(def-struct-end)

	(struct 'myapp 'local)
	(ptr 'msg 'window 'flow_panel 'grid_panel 'button 'pressed)
	(pubyte 'next)
	(ulong 'owner)
	(int 'width 'height)
	(uint 'length)

	;init app vars
	(push-scope)
	(f-call 'sys_mem 'clear {&myapp, local_size})
	(f-call 'obj 'init (cat {&myapp, @} (f-path 'class 'obj)))

	;create my window
	(f-call 'window 'create {window_flag_close} {window})
	(f-call 'window 'set_title {window, "Calculator"})

	;add my app flow panel
	(f-call 'flow 'create {} {flow_panel})
	(f-call 'flow 'set_long_prop {flow_panel, prop_flow_flags, flow_flag_down | flow_flag_fillw | flow_flag_lasth})
	(v-call 'window 'add_child {window, flow_panel})

	;add my display label
	(f-call 'label 'create {} {myapp.local_display})
	(f-call 'label 'set_long_prop {myapp.local_display, prop_color, 0xffffffff})
	(f-call 'label 'set_long_prop {myapp.local_display, prop_flow_flags, flow_flag_align_hright | flow_flag_align_vcenter})
	(f-call 'label 'set_font_prop {myapp.local_display, prop_font, "fonts/OpenSans-Regular.ttf", 24})
	(f-call 'label 'set_string_prop {myapp.local_display, prop_text, "0"})
	(v-call 'flow 'add_child {flow_panel, myapp.local_display})

	;add my app grid panel
	(f-call 'grid 'create {} {grid_panel})
	(f-call 'grid 'set_long_prop {grid_panel, prop_grid_width, 4})
	(f-call 'grid 'set_long_prop {grid_panel, prop_grid_height, 4})
	(v-call 'flow 'add_child {flow_panel, grid_panel})

	;add buttons to my grid panel
	(assign {$button_list} {next})
	(loop-while {*next})
		(f-call 'button 'create {} {button})
		(f-call 'button 'set_long_prop {button, prop_color, 0xffffff00})
		(f-call 'button 'set_string_prop {button, prop_text, next})
		(f-call 'button 'set_font_prop {button, prop_font, "fonts/OpenSans-Regular.ttf", 42})
		(f-call 'button 'set_long_prop {button, prop_flow_flags, flow_flag_align_hcenter | flow_flag_align_vcenter})
		(v-call 'grid 'add_child {grid_panel, button})
		(f-call 'button 'sig_pressed {button} {_, pressed})
		(f-call 'button 'connect {button, pressed, &myapp, $on_press})
		(f-call 'sys_string 'length {next} {_, length})
		(assign {next + length + byte_size} {next})
	(loop-end)

	;set to pref size
	(v-call 'window 'pref_size {window} {_, width, height})
	(f-call 'window 'change {window, 920, 48, width, height})

	;set window owner
	(f-call 'sys_task 'tcb {} {owner})
	(f-call 'window 'set_owner {window, owner})

	;set close action
	(f-call 'window 'connect_close {window, &myapp, $on_window_close})

	;add to screen
	(f-call 'gui_gui 'add {window})

	;app event loop
	(loop-start)
		(f-call 'sys_mail 'mymail {} {msg})

		;dispatch event to view
		(v-call 'view 'event {msg->ev_msg_view, msg})

		;free event message
		(f-call 'sys_mem 'free {msg})
	(loop-until {myapp.local_closed})

	;deref window
	(f-call 'window 'deref {window})
	(v-call 'obj 'deinit {&myapp})

	(pop-scope)
	(return)

(vp-label 'on_press)
	;inputs
	;r0 = app local object
	;r1 = button object

	(ptr 'this 'button 'button_string 'display_string 'string 'string1 'string2)
	(uint 'char)

	;save inputs
	(push-scope)
	(l-entry {this, button})

	(f-call 'button 'ref_obj_prop {button, prop_text} {_, button_string})
	(vpif {button_string->string_length == 2})
		;AC
		(f-call 'label 'set_string_prop {this->local_display, prop_text, "0"})
		(assign {0} {this->local_accum})
		(assign {0} {this->local_value})
		(assign {0} {this->local_last_op})
		(assign {0} {this->local_last_flag})
	(else)
		(assign {ubyte (button_string->string_data)} {char})
		(vpif {char >= char_0 && char <= char_9})
			;numeral
			(f-call 'label 'ref_obj_prop {this->local_display, prop_text} {_, display_string})
			(assign {ubyte (display_string->string_data)} {char})
			(vpif {char == char_0 || this->local_last_flag == 0})
				;clear it
				(f-call 'string 'deref {display_string})
				(f-call 'string 'create_from_cstr {""} {display_string})
				(assign {1} {this->local_last_flag})
			(endif)
			;append numeral
			(f-call 'string 'append {display_string, button_string} {string})
			(f-call 'sys_string 'to_long {&string->string_data, 10} {this->local_value})
			(f-call 'string 'deref {display_string})
		(else)
			;operator
			(vpif {this->local_last_op == char_plus})
				;+
				(assign {this->local_accum + this->local_value} {this->local_accum})
			(elseif {this->local_last_op == char_minus})
				;-
				(assign {this->local_accum - this->local_value} {this->local_accum})
			(elseif {this->local_last_op == char_multiply})
				;*
				(assign {this->local_accum * this->local_value} {this->local_accum})
			(elseif {this->local_last_op == char_divide && this->local_value})
				;/
				(assign {this->local_accum // this->local_value} {this->local_accum})
			(else)
				;equals
				(assign {this->local_value} {this->local_accum})
			(endif)
			(vpif {char != char_equal})
				(assign {char} {this->local_last_op})
			(endif)
			(assign {0} {this->local_last_flag})
			(vpif {this->local_accum < 0})
				;negative accum
				(f-call 'sys_string 'from_long {- this->local_accum, &this->local_buffer, 10})
				(f-call 'string 'create_from_cstr {"-"} {string1})
				(f-call 'string 'create_from_cstr {&this->local_buffer} {string2})
				(f-call 'string 'append {string1, string2} {string})
				(f-call 'string 'deref {string1})
				(f-call 'string 'deref {string2})
			(else)
				;positive accum
				(f-call 'sys_string 'from_long {this->local_accum, &this->local_buffer, 10})
				(f-call 'string 'create_from_cstr {&this->local_buffer} {string})
			(endif)
		(endif)
		(f-call 'label 'set_string_prop {this->local_display, prop_text, &string->string_data})
		(f-call 'string 'deref {string})
	(endif)
	(f-call 'string 'deref {button_string})
	(v-call 'label 'layout {this->local_display})
	(f-call 'label 'dirty {this->local_display})
	(pop-scope)
	(return)

(vp-label 'on_window_close)
	;inputs
	;r0 = app local object
	;r1 = close object

	(ptr 'this 'src)

	(push-scope)
	(l-entry {this, src})
	(assign {1} {this->local_closed})
	(pop-scope)
	(return)

(vp-label 'button_list)
	(vp-string "7") (vp-byte 0)
	(vp-string "8") (vp-byte 0)
	(vp-string "9") (vp-byte 0)
	(vp-string "/") (vp-byte 0)
	(vp-string "4") (vp-byte 0)
	(vp-string "5") (vp-byte 0)
	(vp-string "6") (vp-byte 0)
	(vp-string "*") (vp-byte 0)
	(vp-string "1") (vp-byte 0)
	(vp-string "2") (vp-byte 0)
	(vp-string "3") (vp-byte 0)
	(vp-string "-") (vp-byte 0)
	(vp-string "0") (vp-byte 0)
	(vp-string "=") (vp-byte 0)
	(vp-string "AC") (vp-byte 0)
	(vp-string "+") (vp-byte 0)
	(vp-byte 0)

(def-func-end)
