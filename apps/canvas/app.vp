(import 'inc/func.inc)
(import 'inc/mail.inc)
(import 'inc/gui.inc)
(import 'class/window/window.inc)
(import 'class/string/string.inc)
(import 'class/canvas/canvas.inc)
(import 'class/vector/vector.inc)
(import 'class/stream/stream.inc)
(import 'class/stream_str/stream_str.inc)
(import 'class/unordered_map/unordered_map.inc)
(import 'class/lisp/lisp.inc)

(def-struct 'canvas_msg 'msg_header)
	(ptr 'arg0)
(def-struct-end)

(def-func 'apps/canvas/app)

	(def-struct 'local 'obj)
		(uint 'closed)
	(def-struct-end)

	(struct 'myapp 'local)
	(ptr 'msg 'window 'canvas 'string)
	(struct 'lisp_id 'id)
	(ulong 'owner)
	(int 'width 'height)

	;init app vars
	(push-scope)
	(f-call 'sys_mem 'clear {&myapp, local_size})
	(f-call 'obj 'init (cat {&myapp, @} (f-path 'class 'obj)))

	;create my window
	(f-call 'window 'create {window_flag_close} {window})
	(f-call 'string 'create_from_cstr {"Canvas"} {string})
	(f-call 'window 'set_title {window, string})

	;add my app canvas
	(f-call 'canvas 'create {512, 512} {canvas})
	(v-call 'window 'add_child {window, canvas})

	;set to pref size
	(v-call 'window 'pref_size {window} {_, width, height})
	(f-call 'window 'change {window, 512, 256, width, height})

	;set window owner
	(f-call 'sys_task 'tcb {} {owner})
	(f-call 'window 'set_owner {window, owner})

	;set close action
	(f-call 'window 'connect_close {window, &myapp, $on_window_close})

	;add to screen and dirty
	(f-call 'gui_gui 'add {window})
	(f-call 'window 'dirty_all {window})

	;clear canvas
	(f-call 'canvas 'fill {canvas, 0xff000000})
	(f-call 'canvas 'swap {canvas})

	;start lisp task and send it args
	(f-call 'sys_task 'start {@apps/canvas/lisp} {_, lisp_id.id_mbox})
	(f-call 'sys_task 'mailbox {} {_, lisp_id.id_cpu})
	(f-call 'sys_mail 'alloc {} {msg})
	(assign {lisp_id.id_mbox} {msg->msg_dest.id_mbox})
	(assign {lisp_id.id_cpu} {msg->msg_dest.id_cpu})
	(assign {canvas} {msg->canvas_msg_arg0})
	(f-call 'canvas 'ref {canvas})
	(assign {canvas_msg_size} {msg->msg_length})
	(f-call 'sys_mail 'send {msg})

	;app event loop
	(loop-start)
		(f-call 'sys_mail 'mymail {} {msg})

		;dispatch event to view
		(v-call 'view 'event {msg->ev_msg_view, msg})

		;free event message
		(f-call 'sys_mem 'free {msg})
	(loop-until {myapp.local_closed})

	;deref window
	(f-call 'window 'deref {window})
	(v-call 'obj 'deinit {&myapp})

	(pop-scope)
	(return)

(vp-label 'on_window_close)
	;inputs
	;r0 = app local object
	;r1 = close object

	(ptr 'this)

	(push-scope)
	(assign '(r0) {this})
	(assign {1} {this->local_closed})
	(pop-scope)
	(return)

(def-func-end)

(def-func 'apps/canvas/lisp 8192)

	(ptr 'lisp 'stream 'file 'args 'value 'msg)

	;init app vars
	(push-scope)

	;string output stream
	(f-call 'string 'create_raw {string_gap} {value})
	(f-call 'stream_str 'create {value} {stream})

	;create lisp class
	(f-call 'lisp 'create {stream, stream, stream} {lisp})
	(f-call 'ref 'deref {stream})

	;read canvas msg create argv
	(f-call 'sys_mail 'mymail {} {msg})
	(f-call 'vector 'create {} {args})
	(f-call 'vector 'push_back {args, msg->canvas_msg_arg0})
	(f-call 'sys_mem 'free {msg})
	(f-call 'lisp 'sym_intern_cstr {lisp, "argv"} {_, value})
	(f-call 'unordered_map 'insert {lisp->lisp_enviroment, value, args})
	(f-call 'ref 'deref {value})
	(f-call 'ref 'deref {args})

	;run any canvas.lisp
	(f-call 'string 'create_from_file {"apps/canvas/canvas.lisp"} {file})
	(vpif {file})
		;REPL from file stream
		(f-call 'vector 'create {} {args})
		(f-call 'stream_str 'create {file} {stream})
		(f-call 'string 'create_from_cstr {"apps/canvas/canvas.lisp"} {file})
		(f-call 'vector 'push_back {args, stream})
		(f-call 'vector 'push_back {args, file})
		(f-call 'lisp 'func_repl {lisp, args} {_, value})
		(vpif (cat {value->obj_vtable == @} (f-path 'class 'error)))
			(f-call 'lisp 'repl_print {lisp, lisp->lisp_stderr, value})
			(f-call 'stream 'write_char {lisp->lisp_stderr, char_lf})
			(v-call 'stream 'write_flush {lisp->lisp_stderr})
		(endif)
		(f-call 'ref 'deref {value})
		(f-call 'vector 'deref {args})

		;output
		(f-call 'stream_str 'ref_string {lisp->lisp_stdout} {_, value})
		(vpif {value->string_length})
			(f-call 'sys_io 'string {&value->string_data, 1})
			(f-call 'sys_io 'char {char_lf, 1})
		(endif)
		(f-call 'ref 'deref {value})
	(endif)

	;clean up
	(f-call 'lisp 'deref {lisp})

	(pop-scope)
	(return)

(def-func-end)
