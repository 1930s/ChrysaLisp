(import 'sys/func.inc)
(import 'gui/gui.inc)
(import 'sys/string/string.inc)
(import 'class/window/window.inc)
(import 'class/button/button.inc)
(import 'class/string/string.inc)
(import 'class/vector/vector.inc)
(import 'class/msg_out/msg_out.inc)
(import 'class/msg_in/msg_in.inc)
(import 'class/pipe/pipe.inc)
(import 'class/vdu/vdu.inc)

(def-func 'apps/terminal/app)

	(defcvar 'buf_size 256)

	(def-struct 'local)
		(ptr 'pipe 'vdu 'history 'window)
		(ulong 'history_index)
		(pubyte 'bufp)
		(struct 'buffer buf_size)
		(uint 'closed)
	(def-struct-end)

	(struct 'myapp 'local)
	(ptr 'msg 'data 'mymailbox 'mailbox)
	(long 'state)
	(int 'width 'height)

	;init app vars
	(push-scope)
	(f-call 'vector 'create {} {myapp.local_history})
	(assign {0} {myapp.local_pipe})

	;create my window
	(f-call 'window 'create {window_flag_status | window_flag_close} {myapp.local_window})
	(f-call 'window 'set_title {myapp.local_window, "Terminal"})
	(f-call 'window 'set_status {myapp.local_window, "Ready"})
	(f-call 'window 'set_long_prop {myapp.local_window, prop_color, 0xc0000000})

	;add my vdu
	(f-call 'vdu 'create {} {myapp.local_vdu})
	(f-call 'vdu 'set_long_prop {myapp.local_vdu, prop_text_color, 0xff00ff00})
	(f-call 'vdu 'set_long_prop {myapp.local_vdu, prop_vdu_width, 60})
	(f-call 'vdu 'set_long_prop {myapp.local_vdu, prop_vdu_height, 40})
	(f-call 'vdu 'set_font_prop {myapp.local_vdu, prop_font, "fonts/Hack-Regular.ttf", 16})
	(v-call 'window 'add_child {myapp.local_window, myapp.local_vdu})

	;set to pref size
	(v-call 'window 'pref_size {myapp.local_window} {_, width, height})
	(f-call 'window 'change {myapp.local_window, 0, 0, width, height})

	;set close action
	(f-call 'window 'connect_close {myapp.local_window, 0})

	;add to screen and print sign on
	(f-call 'gui_gui 'add {myapp.local_window})
	(f-call 'vdu 'print_cstr {myapp.local_vdu, "ChrysaLisp Terminal 1.3"})
	(f-call 'vdu 'print_char {myapp.local_vdu, char_lf})

	;set up term buffer
	(assign {&myapp.local_buffer} {myapp.local_bufp})

	;app event loop
	(f-call 'sys_task 'mailbox {} {mymailbox, _})
	(loop-start 'exit)
		(vpif {myapp.local_pipe})
			;select on multiple mailboxes
			(f-call 'pipe 'select {myapp.local_pipe, mymailbox} {_, mailbox})

			;which mailbox has mail ?
			(gotoif {mailbox == mymailbox} 'from_mymailbox)

			;output from a pipe element
			(l-call 'pipe_output {&myapp, mailbox - msg_in_mailbox} {state})
			(vpif {state == -1})
				;EOF
				(f-call 'pipe 'deref {myapp.local_pipe})
				(assign {0} {myapp.local_pipe})

				;update status
				(f-call 'window 'set_status {myapp.local_window, "Ready"})
			(endif)
		(else)
		(vp-label 'from_mymailbox)
			(f-call 'sys_mail 'read {mymailbox} {msg, data})
			(breakifnot {data->ev_msg_target_id} exit)
			(v-call 'view 'event {myapp.local_window, data})
			(vpif {data->ev_msg_type == ev_type_key && data->ev_msg_key_keycode > 0})
				(l-call 'terminal_input {&myapp, data->ev_msg_key_key})
			(endif)
			(f-call 'sys_mail 'free {msg})
		(endif)
		(f-call 'sys_task 'yield)
	(loop-until {myapp.local_closed})
	(f-call 'sys_mail 'free {msg})

	;clean up
	(f-call 'pipe 'deref_if {myapp.local_pipe})
	(f-call 'vector 'deref {myapp.local_history})
	(f-call 'window 'deref {myapp.local_window})
	(pop-scope)
	(return)

(vp-label 'pipe_output)
	;inputs
	;r0 = local
	;r1 = stream object (ptr)
	;outputs
	;r0 = -1 if EOF

	(ptr 'local 'stream)
	(int 'char 'ready)

	(push-scope)
	(l-entry {local, stream})

	(loop-start)
		(f-call 'stream 'read_char {stream} {_, char})
		(breakif {char == -1})
		(l-call 'terminal_output {local, char})
		(v-call 'stream 'read_ready {stream} {_, ready})
	(loop-untilnot {ready})

	(l-exit {char})
	(pop-scope)
	(return)

(vp-label 'terminal_input)
	;inputs
	;r0 = local
	;r1 = char input

	(ptr 'local 'string 'last 'stream)
	(ulong 'length 'notsame)
	(int 'char)

	(push-scope)
	(l-entry {local, char})

	;echo char to terminal
	(l-call 'terminal_output {local, char})

	;buffer char
	(assign {char} {*local->local_bufp})
	(assign {local->local_bufp - &local->local_buffer} {length})

	;send line ?
	(vpif {char == char_lf || char == char_cr})
		;what state ?
		(vpifnot {local->local_pipe})
			;push new history entry if not same as last entry
			(breakifnot {length})
			(f-call 'string 'create_from_buffer {&local->local_buffer, length} {string})
			(d-call 'vector 'get_length {local->local_history} {_, local->local_history_index})
			(vpifnot {local->local_history_index})
			(vp-label 'new_entry)
				(f-call 'vector 'push_back {local->local_history, string})
				(assign {local->local_history_index + 1} {local->local_history_index})
			(else)
				(d-call 'vector 'ref_back {local->local_history} {_, last})
				(f-call 'string 'same {string, last} {_, notsame})
				(f-call 'string 'deref {last})
				(gotoif {notsame} 'new_entry)
				(f-call 'string 'deref {string})
			(endif)

			;start new pipe
			(f-call 'pipe 'create {&local->local_buffer, length} {local->local_pipe})
			(breakifnot {local->local_pipe})
			(f-call 'window 'set_status {local->local_window, "Busy"})
		(else)
			;feed active pipe
			(f-call 'pipe 'get_input {local->local_pipe} {_, stream})
			(f-call 'stream 'write {stream, &local->local_buffer, length})
			(f-call 'stream 'write_char {stream, char_lf})
			(v-call 'stream 'write_flush {stream})
		(endif)
		(assign {&local->local_buffer} {local->local_bufp})
	(elseif {char == char_backspace})
		;backspace
		(vpif {length})
			(assign {local->local_bufp - 1} {local->local_bufp})
		(endif)
	(elseif {char == 0x40000052})
		;cursor up
		(vpifnot {local->local_pipe})
			(d-call 'vector 'get_length {local->local_history} {_, length})
			(breakifnot {length})
			(vpif {local->local_history_index})
				(assign {local->local_history_index - 1} {local->local_history_index})
			(endif)
			(d-call 'vector 'ref_element {local->local_history, local->local_history_index} {_, string})
			(f-call 'sys_mem 'copy {&string->string_data, &local->local_buffer, string->string_length} {_, local->local_bufp})
			(f-call 'stream 'create {string, 0, &string->string_data, string->string_length} {stream})
			(l-call 'pipe_output {local, stream})
			(f-call 'stream 'deref {stream})
		(endif)
	(elseif {char == 0x40000051})
		;cursor down
		(vpifnot {local->local_pipe})
			(d-call 'vector 'get_length {local->local_history} {_, length})
			(assign {local->local_history_index + 1} {local->local_history_index})
			(vpif {local->local_history_index > length})
				(assign {length} {local->local_history_index})
			(endif)
			(vpif {local->local_history_index == length})
				(f-call 'string 'create_from_cstr {""} {string})
			(else)
				(d-call 'vector 'ref_element {local->local_history, local->local_history_index} {_, string})
			(endif)
			(f-call 'sys_mem 'copy {&string->string_data, &local->local_buffer, string->string_length} {_, local->local_bufp})
			(f-call 'stream 'create {string, 0, &string->string_data, string->string_length} {stream})
			(l-call 'pipe_output {local, stream})
			(f-call 'stream 'deref {stream})
		(endif)
	(elseif {char == char_esc})
		;esc
		(vpif {local->local_pipe})
			;feed active pipe, then EOF
			(f-call 'pipe 'get_input {local->local_pipe} {_, stream})
			(f-call 'stream 'write {stream, &local->local_buffer, length})
			(v-call 'stream 'write_flush {stream})
			(assign {&local->local_buffer} {local->local_bufp})

			;stop pipe
			(f-call 'pipe 'deref {local->local_pipe})
			(assign {0} {local->local_pipe})

			;update status
			(f-call 'window 'set_status {local->local_window, "Ready"})
		(endif)
	(elseifnot {char < char_space || char >= 127 || length == buf_size})
		;next char
		(assign {local->local_bufp + 1} {local->local_bufp})
	(endif)
	(pop-scope)
	(return)

(vp-label 'terminal_output)
	;inputs
	;r0 = local
	;r1 = char output

	(ptr 'local)
	(int 'char)

	(push-scope)
	(l-entry {local, char})

	(vpif {char == char_cr})
		(assign {char_lf} {char})
	(endif)
	(vpif {char == 0x40000052 || char == 0x40000051})
		;cursor up/down
		(f-call 'vdu 'print_char {local->local_vdu, 129})
	(else)
		;print char
		(f-call 'vdu 'print_char {local->local_vdu, char})
	(endif)

	(pop-scope)
	(return)

(def-func-end)
