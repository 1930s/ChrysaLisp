(import 'inc/func.inc)
(import 'inc/gui.inc)
(import 'inc/string.inc)
(import 'class/window/window.inc)
(import 'class/button/button.inc)
(import 'class/string/string.inc)
(import 'class/vector/vector.inc)
(import 'class/stream_msg_out/stream_msg_out.inc)
(import 'class/master/master.inc)
(import 'class/vdu/vdu.inc)

(def-func 'apps/terminal/app)

	(def-struct 'shared 'obj)
		(ptr 'shared_master 'shared_vdu 'shared_history 'shared_window)
		(ulong 'shared_history_index)
		(pubyte 'shared_bufp)
		(struct 'shared_buffer 120)
		(ubyte 'shared_closed)
	(def-struct-end)

	(struct 'myapp 'shared)
	(ptr 'msg 'stream 'label 'string 'mymailbox 'mailbox)
	(ulong 'owner 'length)
	(long 'state)
	(int 'width 'height)

	;init app vars
	(push-scope)
	(f-call 'sys_mem 'clear {&myapp, shared_size} {_})
	(f-call 'obj 'init (cat {&myapp, @} (f-path 'class 'obj)) '(_ _))
	(f-call 'master 'create {} {myapp.shared_master})
	(f-call 'vector 'create {} {myapp.shared_history})

	;create my window
	(f-call 'window 'create {window_flag_status | window_flag_close} {myapp.shared_window})
	(f-call 'string 'create_from_cstr {"Terminal"} {string})
	(f-call 'window 'set_title {myapp.shared_window, string})
	(f-call 'string 'create_from_cstr {"Ready"} {string})
	(f-call 'window 'set_status {myapp.shared_window, string})
	(f-call 'window 'set_color {myapp.shared_window, 0xc0000000})

	;add my vdu
	(f-call 'vdu 'create {} {myapp.shared_vdu})
	(f-call 'vdu 'set_text_color {myapp.shared_vdu, 0xff00ff00} '(_))
	(f-call 'vdu 'set_font {myapp.shared_vdu, "fonts/Hack-Regular.ttf", 16} '(_))
	(v-call 'window 'add_child {myapp.shared_window, myapp.shared_vdu})

	;set to pref size
	(v-call 'window 'pref_size {myapp.shared_window} {width, height})
	(f-call 'window 'change {myapp.shared_window, 0, 0, width, height})

	;set window owner
	(f-call 'sys_task 'tcb {} {owner})
	(f-call 'window 'set_owner {myapp.shared_window, owner})

	;set close action
	(f-call 'window 'connect_close {myapp.shared_window, &myapp, $on_window_close})

	;add to screen and dirty
	(f-call 'gui_gui 'add {myapp.shared_window})
	(f-call 'window 'dirty_all {myapp.shared_window})

	;set up term buffer
	(assign {&myapp.shared_buffer} {myapp.shared_bufp})

	;app event loop
	(f-call 'sys_task 'mailbox {} {mymailbox, _})
	(loop-start)
		;select on multiple mailboxes
		(f-call 'master 'select {myapp.shared_master, mymailbox} {_, mailbox})

		;which mailbox has mail ?
		(vpif {mailbox == mymailbox})
			;dispatch event to view and terminal
			(f-call 'sys_mail 'read {mailbox} {msg})
			(v-call 'view 'event {msg->ev_msg_view, msg})
			(vpif {msg->ev_msg_type == ev_type_key && msg->ev_msg_keycode > 0})
				(l-call 'terminal_input {&myapp, msg->ev_msg_key} '(r0 r1))
			(endif)
			(f-call 'sys_mem 'free {msg})
		(else)
			;output from a pipe element
			(f-call 'master 'get_stream {myapp.shared_master, mailbox} {_, stream})
			(l-call 'pipe_output {&myapp, stream} '(r0 r1) '(r0) {state})
			(vpif {state == -1})
				;EOF
				(f-call 'master 'stop {myapp.shared_master} '(_))
				(f-call 'string 'create_from_cstr {"Ready"} {string})
				(f-call 'window 'set_status {myapp.shared_window, string})
			(endif)
		(endif)
		(f-call 'sys_task 'yield)
	(loop-until {myapp.shared_closed})

	;clean up
	(f-call 'vector 'deref {myapp.shared_history} '(_))
	(f-call 'master 'deref {myapp.shared_master} '(_))
	(f-call 'window 'deref {myapp.shared_window} '(_))
	(v-call 'obj 'deinit {&myapp})
	(pop-scope)
	(return)

(vp-label 'on_window_close)
	;inputs
	;r0 = app local object
	;r1 = close object

	(ptr 'inst)

	(push-scope)
	(assign '(r0) {inst})
	(assign {1} {inst->shared_closed})
	(pop-scope)
	(return)

(vp-label 'pipe_output)
	;inputs
	;r0 = shared
	;r1 = stream
	;outputs
	;r0 = -1 if EOF

	(ptr 'shared 'stream)
	(long 'char)
	(ulong 'ready)

	(push-scope)
	(assign '(r0 r1) {shared, stream})

	(loop-start)
		(f-call 'stream 'read_char {stream} {char})
		(breakif {char == -1})
		(l-call 'terminal_output {shared, char} '(r0 r1))
		(v-call 'stream 'read_ready {stream} {ready})
	(loop-untilnot {ready})

	(assign {char} '(r0))
	(pop-scope)
	(return)

(vp-label 'terminal_input)
	;inputs
	;r0 = shared
	;r1 = char input

	(ptr 'shared 'string 'last 'stream)
	(ulong 'length 'notsame)
	(ubyte 'char 'state)

	(push-scope)
	(assign '(r0 r1) {shared, char})

	;echo char to terminal
	(l-call 'terminal_output {shared, char} '(r0 r1))

	;buffer char
	(assign {char} {*shared->shared_bufp})
	(assign {shared->shared_bufp - &shared->shared_buffer} {length})

	;send line ?
	(vpif {char == 10 || char == 13})
		;what state ?
		(f-call 'master 'get_state {shared->shared_master} {_, state})
		(vpif {state == stream_mail_state_stopped})
			;push new history entry if not same as last entry
			(breakifnot {length})
			(f-call 'string 'create_from_buffer {&shared->shared_buffer, length} {string})
			(d-call 'vector 'get_length {shared->shared_history} {_, shared->shared_history_index})
			(vpifnot {shared->shared_history_index})
			(vp-label 'new_entry)
				(f-call 'vector 'push_back {shared->shared_history, string})
				(assign {shared->shared_history_index + 1} {shared->shared_history_index})
			(else)
				(f-call 'vector 'ref_back {shared->shared_history} {last})
				(f-call 'string 'same {string, last} {_, notsame})
				(f-call 'string 'deref {last} '(_))
				(gotoif {notsame} 'new_entry)
				(f-call 'string 'deref {string} '(_))
			(endif)

			;start new pipe
			(f-call 'master 'start {shared->shared_master, &shared->shared_buffer, length} '(_))
			(f-call 'master 'get_state {shared->shared_master} {_, state})
			(breakif {state != stream_mail_state_started})
			(f-call 'string 'create_from_cstr {"Busy"} {string})
			(f-call 'window 'set_status {shared->shared_window, string})
		(else)
			;feed active pipe
			(f-call 'master 'get_input {shared->shared_master} {_, stream})
			(f-call 'stream 'write {stream, &shared->shared_buffer, length})
			(f-call 'stream 'write_char {stream, 10})
			(v-call 'stream 'write_flush {stream})
		(endif)
		(assign {&shared->shared_buffer} {shared->shared_bufp})
	(elseif {char == 128})
		;backspace
		(vpif {length})
			(assign {shared->shared_bufp - 1} {shared->shared_bufp})
		(endif)
	(elseif {char == 129})
		;cursor up
		(f-call 'master 'get_state {shared->shared_master} {_, state})
		(vpif {state == stream_mail_state_stopped})
			(d-call 'vector 'get_length {shared->shared_history} {_, length})
			(breakifnot {length})
			(vpif {shared->shared_history_index})
				(assign {shared->shared_history_index - 1} {shared->shared_history_index})
			(endif)
			(d-call 'vector 'ref_element {shared->shared_history, shared->shared_history_index} {_, string})
			(f-call 'sys_mem 'copy {&string->string_data, &shared->shared_buffer, string->string_length} {_, shared->shared_bufp})
			(f-call 'stream 'create {string, 0, &string->string_data, string->string_length} {stream})
			(l-call 'pipe_output {shared, stream} '(r0 r1))
			(f-call 'stream 'deref {stream} '(_))
		(endif)
	(elseif {char == 130})
		;cursor down
		(f-call 'master 'get_state {shared->shared_master} {_, state})
		(vpif {state == stream_mail_state_stopped})
			(d-call 'vector 'get_length {shared->shared_history} {_, length})
			(assign {shared->shared_history_index + 1} {shared->shared_history_index})
			(vpif {shared->shared_history_index > length})
				(assign {length} {shared->shared_history_index})
			(endif)
			(vpif {shared->shared_history_index == length})
				(f-call 'string 'create_from_cstr {""} {string})
			(else)
				(d-call 'vector 'ref_element {shared->shared_history, shared->shared_history_index} {_, string})
			(endif)
			(f-call 'sys_mem 'copy {&string->string_data, &shared->shared_buffer, string->string_length} {_, shared->shared_bufp})
			(f-call 'stream 'create {string, 0, &string->string_data, string->string_length} {stream})
			(l-call 'pipe_output {shared, stream} '(r0 r1))
			(f-call 'stream 'deref {stream} '(_))
		(endif)
	(elseif {char == 27})
		;esc
		(f-call 'master 'get_state {shared->shared_master} {_, state})
		(vpif {state == stream_mail_state_started})
			;feed active pipe, then EOF
			(f-call 'master 'get_input {shared->shared_master} {_, stream})
			(f-call 'stream 'write {stream, &shared->shared_buffer, length})
			(v-call 'stream 'write_flush {stream})
			(assign {&shared->shared_buffer} {shared->shared_bufp})

			;send stopping
			(f-call 'stream_msg_out 'set_state {stream, stream_mail_state_stopping})
			(v-call 'stream 'write_next {stream})
			(v-call 'stream 'write_flush {stream})
		(endif)
	(elseif {char >= 32 && char < 127})
		;next char
		(assign {shared->shared_bufp + 1} {shared->shared_bufp})
	(endif)
	(pop-scope)
	(return)

(vp-label 'terminal_output)
	;inputs
	;r0 = shared
	;r1 = char output

	(ptr 'shared 'label 'string 'line_string 'new_line_string)
	(ulong 'length 'char)

	(push-scope)
	(assign '(r0 r1) {shared, char})
	(vpif {char == 13})
		(assign {10} {char})
	(endif)
	(vpif {char == 129 || char == 130})
		;cursor up/down
		(f-call 'vdu 'print_char {shared->shared_vdu, 129})
	(else)
		;print char
		(f-call 'vdu 'print_char {shared->shared_vdu, char})
	(endif)
	(pop-scope)
	(return)

(def-func-end)
