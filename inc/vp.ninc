%ifndef VP_1234
%define VP_1234

;;;;;;;;;;;;;;;;
; vp code macros
;;;;;;;;;;;;;;;;

%use altreg
;r0 rax
;r1 rcx
;r2 rdx
;r3 rbx
;r4 rsp
;r5 rbp
;r6 rsi
;r7 rdi

%macro reg? 1-17 r0, r1, r2, r3, r4, r5, r6, r7, r8, r9, r10, r11, r12, r13, r14, r15
	%xdefine %%t %1
	%assign _r -1
	%assign %%c 0
	%rep %0 - 1
		%rotate 1
		%ifidn %1, %%t
			%assign _r %%c
			%exitrep
		%endif
		%assign %%c %%c + 1
	%endrep
%endmacro

%macro reg_b? 1-17 r0, al, r1, cl, r2, dl, r3, bl, r4, r4b, r5, r5b, r6, r6b, r7, r7b
	%xdefine %%t %1
	%xdefine _rb %1b
	%rotate 1
	%rep (%0 - 1) / 2
		%ifidn %1, %%t
			%xdefine _rb %2
			%exitrep
		%endif
		%rotate 2
	%endrep
%endmacro

%macro reg_s? 1-17 r0, ax, r1, cx, r2, dx, r3, bx, r4, r4w, r5, r5w, r6, r6w, r7, r7w
	%xdefine %%t %1
	%xdefine _rs %1w
	%rotate 1
	%rep (%0 - 1) / 2
		%ifidn %1, %%t
			%xdefine _rs %2
			%exitrep
		%endif
		%rotate 2
	%endrep
%endmacro

%macro reg_i? 1-17 r0, eax, r1, ecx, r2, edx, r3, ebx, r4, r4d, r5, r5d, r6, r6d, r7, r7d
	%xdefine %%t %1
	%xdefine _ri %1d
	%rotate 1
	%rep (%0 - 1) / 2
		%ifidn %1, %%t
			%xdefine _ri %2
			%exitrep
		%endif
		%rotate 2
	%endrep
%endmacro

%macro vp_cpy 2
	%ifnidn %1, %2
		%ifnum %1
			%if %1 = 0
				xor %2, %2
			%else
				mov %2, %1
			%endif
		%else
			mov %2, %1
		%endif
	%endif
%endmacro

%macro vp_cpy_b 2
	reg? %1
	%if _r < 0
		;source not register
		reg? %2
		%if _r < 0
			;dest not register
			%fatal Not valid VP mode
		%else
			;dest is register
			movsx %2, byte %1
		%endif
	%else
		;source is register
		reg_b? %1
		%xdefine %%s _rb
		reg? %2
		%if _r < 0
			;dest not register
			mov %2, %%s
		%else
			;dest is register
			%fatal Not valid VP mode
		%endif
	%endif
%endmacro

%macro vp_cpy_ub 2
	reg? %1
	%if _r < 0
		;source not register
		reg? %2
		%if _r < 0
			;dest not register
			%fatal Not valid VP mode
		%else
			;dest is register
			movzx %2, byte %1
		%endif
	%else
		;source is register
		reg_b? %1
		%xdefine %%s _rb
		reg? %2
		%if _r < 0
			;dest not register
			mov %2, %%s
		%else
			;dest is register
			%fatal Not valid VP mode
		%endif
	%endif
%endmacro

%macro vp_cpy_s 2
	reg? %1
	%if _r < 0
		;source not register
		reg? %2
		%if _r < 0
			;dest not register
			%fatal Not valid VP mode
		%else
			;dest is register
			movsx %2, word %1
		%endif
	%else
		;source is register
		reg_s? %1
		%xdefine %%s _rs
		reg? %2
		%if _r < 0
			;dest not register
			mov word %2, %%s
		%else
			;dest is register
			%fatal Not valid VP mode
		%endif
	%endif
%endmacro

%macro vp_cpy_us 2
	reg? %1
	%if _r < 0
		;source not register
		reg? %2
		%if _r < 0
			;dest not register
			%fatal Not valid VP mode
		%else
			;dest is register
			movzx %2, word %1
		%endif
	%else
		;source is register
		reg_s? %1
		%xdefine %%s _rs
		reg? %2
		%if _r < 0
			;dest not register
			mov word %2, %%s
		%else
			;dest is register
			%fatal Not valid VP mode
		%endif
	%endif
%endmacro

%macro vp_cpy_i 2
	reg? %1
	%if _r < 0
		;source not register
		reg? %2
		%if _r < 0
			;dest not register
			%fatal Not valid VP mode
		%else
			;dest is register
			movsx %2, dword %1
		%endif
	%else
		;source is register
		reg_i? %1
		%xdefine %%s _ri
		reg? %2
		%if _r < 0
			;dest not register
			mov dword %2, %%s
		%else
			;dest is register
			%fatal Not valid VP mode
		%endif
	%endif
%endmacro

%macro vp_cpy_ui 2
	reg? %1
	%if _r < 0
		;source not register
		reg? %2
		%if _r < 0
			;dest not register
			%fatal Not valid VP mode
		%else
			;dest is register
			reg_i? %2
			mov _ri, dword %1
		%endif
	%else
		;source is register
		reg_i? %1
		%xdefine %%s _ri
		reg? %2
		%if _r < 0
			;dest not register
			mov dword %2, %%s
		%else
			;dest is register
			%fatal Not valid VP mode
		%endif
	%endif
%endmacro

%macro vp_xchg 2
	reg? %1
	%if _r < 0
		;source not register
		%fatal Not valid VP mode
	%endif
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	%ifnidn %1, %2
		xchg %1, %2
	%endif
%endmacro

%macro vp_div 3
	;inputs
	;%1 = divider
	;%2:%3 = dividend high/low
	;outputs
	;%2 = remainder
	;%3 = quotient

	reg? %2
	%xdefine %%r _r
	reg? %3
	%if (%%r = 2) && (_r = 0)
		;high in r2, low in r0
		idiv %1
	%elif (%%r = 0) && (_r = 2) && (%1 != %2) && (%1 != %3) 
		xchg %2, %3
		idiv %1
		xchg %2, %3
	%else
		vp_push %3, %2, %1, r2, r1, r0
		vp_cpy [r4 + 24], r1
		vp_cpy [r4 + 32], r2
		vp_cpy [r4 + 40], r0
		idiv r1
		vp_cpy r2, [r4 + 32]
		vp_cpy r0, [r4 + 40]
		vp_pop %3, %2, %1, r2, r1, r0
	%endif
%endmacro

%macro vp_div_u 3
	;inputs
	;%1 = divider
	;%2:%3 = dividend high/low
	;outputs
	;%2 = remainder
	;%3 = quotient

	reg? %2
	%xdefine %%r _r
	reg? %3
	%if (%%r = 2) && (_r = 0)
		;high in r2, low in r0
		div %1
	%elif (%%r = 0) && (_r = 2) && (%1 != %2) && (%1 != %3) 
		xchg %2, %3
		div %1
		xchg %2, %3
	%else
		vp_push %3, %2, %1, r2, r1, r0
		vp_cpy [r4 + 24], r1
		vp_cpy [r4 + 32], r2
		vp_cpy [r4 + 40], r0
		div r1
		vp_cpy r2, [r4 + 32]
		vp_cpy r0, [r4 + 40]
		vp_pop %3, %2, %1, r2, r1, r0
	%endif
%endmacro

%macro vp_ext 2
	reg? %1
	%if _r = 0
		reg? %2
		%if _r = 2
			cqo
		%else
			vp_xor %2, %2
			vpif %1, <, 0
				vp_dec %2
			endif
		%endif
	%else
		vp_xor %2, %2
		vpif %1, <, 0
			vp_dec %2
		endif
	%endif
%endmacro

%macro vp_mul 2
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	%ifidn %1, -1
		neg %2
	%else
		imul %2, %1
	%endif
%endmacro

%macro vp_call 1
	call %1
%endmacro

%macro vp_ret 0
	ret
%endmacro

%macro vp_jmp 1
	jmp %1
%endmacro

%macro vp_beq 1
	je %1
%endmacro

%macro vp_bne 1
	jne %1
%endmacro

%macro vp_blt 1
	jl %1
%endmacro

%macro vp_ble 1
	jle %1
%endmacro

%macro vp_bgt 1
	jg %1
%endmacro

%macro vp_bge 1
	jge %1
%endmacro

%macro vp_jmpif 4
		vp_cmp %3, %1
	%ifidn %2, ==
		vp_beq %4
	%elifidn %2, !=
		vp_bne %4
	%elifidn %2, <
		vp_blt %4
	%elifidn %2, >
		vp_bgt %4
	%elifidn %2, <=
		vp_ble %4
	%elifidn %2, >=
		vp_bge %4
	%else
		%fatal No such conditional %2 !
	%endif
%endmacro

%macro vp_jmpifnot 4
		vp_cmp %3, %1
	%ifidn %2, ==
		vp_bne %4
	%elifidn %2, !=
		vp_beq %4
	%elifidn %2, <
		vp_bge %4
	%elifidn %2, >
		vp_ble %4
	%elifidn %2, <=
		vp_bgt %4
	%elifidn %2, >=
		vp_blt %4
	%else
		%fatal No such conditional %2 !
	%endif
%endmacro

%macro vp_push 1-*
	%rep %0
		reg? %1
		%if _r < 0
			%fatal Not register %1 !
		%endif
		push %1
		%rotate 1
	%endrep
%endmacro

%macro vp_pop 1-*
	%rep %0
		%rotate -1
		pop %1
	%endrep
%endmacro

%macro vp_lea 2
	lea %2, %1
%endmacro

%macro vp_inc 1
	inc %1
%endmacro

%macro vp_dec 1
	dec %1
%endmacro

%macro vp_add 2
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	add %2, %1
%endmacro

%macro vp_sub 2
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	sub %2, %1
%endmacro

%macro vp_cmp 2
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	cmp %2, %1
%endmacro

%macro vp_and 2
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	%ifnidn %1, %2
		and %2, %1
	%endif
%endmacro

%macro vp_or 2
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	%ifnidn %1, %2
		or %2, %1
	%endif
%endmacro

%macro vp_xor 2
	reg? %2
	%if _r < 0
		;dest not register
		%fatal Not valid VP mode
	%endif
	xor %2, %1
%endmacro

%macro vp_shr 2
	reg? %2
	%if _r < 0
		;dest is not reg
		%fatal Not valid VP mode
	%else
		reg? %1
		%if _r < 0
			;source is not reg
			shr %2, %1
		%elif _r = 1
			;source is r1
			shr %2, cl
		%elifidn %2, r1
			;dest is r1
			vp_xchg %1, %2
			shr %1, cl
			vp_xchg %1, %2
		%else
			;source is not r1
			vp_push %2, %1, r0, r1
			vp_cpy [r4 + 24], r0
			vp_cpy [r4 + 16], r1
			shr r0, cl
			vp_cpy r0, [r4 + 24]
			vp_pop %2, %1, r0, r1
		%endif
	%endif
%endmacro

%macro vp_asr 2
	reg? %2
	%if _r < 0
		;dest is not reg
		%fatal Not valid VP mode
	%else
		reg? %1
		%if _r < 0
			;source is not reg
			sar %2, %1
		%elif _r = 1
			;source is r1
			sar %2, cl
		%elifidn %2, r1
			;dest is r1
			vp_xchg %1, %2
			sar %1, cl
			vp_xchg %1, %2
		%else
			;source is not r1
			vp_push %2, %1, r0, r1
			vp_cpy [r4 + 24], r0
			vp_cpy [r4 + 16], r1
			sar r0, cl
			vp_cpy r0, [r4 + 24]
			vp_pop %2, %1, r0, r1
		%endif
	%endif
%endmacro

%macro vp_shl 2
	reg? %2
	%if _r < 0
		;dest is not reg
		%fatal Not valid VP mode
	%else
		reg? %1
		%if _r < 0
			;source is not reg
			shl %2, %1
		%elif _r = 1
			;source is r1
			shl %2, cl
		%elifidn %2, r1
			;dest is r1
			vp_xchg %1, %2
			shl %1, cl
			vp_xchg %1, %2
		%else
			;source is not r1
			vp_push %2, %1, r0, r1
			vp_cpy [r4 + 24], r0
			vp_cpy [r4 + 16], r1
			shl r0, cl
			vp_cpy r0, [r4 + 24]
			vp_pop %2, %1, r0, r1
		%endif
	%endif
%endmacro

%macro vp_cpy_p 2
	reg? %1
	%if _r < 0
		;source not register
		reg? %2
		%if _r < 0
			;dest not register
			%fatal Not valid VP mode
		%else
			;dest is register
			mov %2, [rel %1]
		%endif
	%else
		;source is register
		reg? %2
		%if _r < 0
			;dest not register
			mov [rel %2], %1
		%else
			;dest is register
			%fatal Not valid VP mode
		%endif
	%endif
%endmacro

%macro vp_call_p 1
	call [rel %1]
%endmacro

%macro vp_jmp_p 1
	jmp [rel %1]
%endmacro

%macro vp_lea_p 2
	lea %2, [rel %1]
%endmacro

%endif
