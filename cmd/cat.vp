(import 'sys/func.inc)
(import 'class/string/string.inc)
(import 'class/stream/stream.inc)
(import 'class/stream_str/stream_str.inc)
(import 'class/vector/vector.inc)
(import 'class/slave/slave.inc)

(def-func 'cmd/cat)

	(ptr 'slave 'arg 'args 'stream)
	(uint 'argc)
	(union
		'(uint 'index)
		'(int 'length))

	;init app vars
	(push-scope)

	;initialize pipe details and command args, abort on error
	(f-call 'slave 'create {} {slave})
	(vpif {slave})
		;cat files to stdout, arg 1 is command name
		(f-call 'slave 'get_args {slave} {_, args})
		(d-call 'vector 'get_length {args} {_, argc})
		(vpif {argc != 1})
			;names from command line
			(assign {1} {index})
			(loop-while {index != argc})
				(d-call 'vector 'ref_element {args, index} {_, arg})
				(l-call 'cat_string {slave, arg} '(r0 r1))
				(assign {index + 1} {index})
			(loop-end)
		(else)
			;names from stdin
			(loop-start)
				(f-call 'stream 'read_line {slave->slave_stdin} {_, arg})
				(breakifnot {arg})
				(l-call 'cat_string {slave, arg} '(r0 r1))
			(loop-end)
		(endif)

		;clean up
		(f-call 'slave 'deref {slave})
	(endif)
	(pop-scope)
	(return)

(vp-label 'cat_string)
	;r0 = slave
	;r1 = arg string

	(ptr 'slave 'arg 'file 'stream)
	(ulong 'char)

	(push-scope)
	(assign '(r0 r1) {slave, arg})

	(f-call 'string 'create_from_file {&arg->string_data} {file})
	(f-call 'string 'deref {arg})
	(vpif {file})
		(f-call 'stream_str 'create {file} {stream})
		(loop-start)
			(f-call 'stream 'read_char {stream} {_, char})
			(breakif {char == -1})
			(f-call 'stream 'write_char {slave->slave_stdout, char})
		(loop-end)
		(v-call 'stream 'write_flush {slave->slave_stdout})
		(f-call 'stream 'deref {stream})
		(f-call 'sys_task 'yield)
	(endif)

	(pop-scope)
	(return)

(def-func-end)
