(import 'sys/func.inc)
(import 'sys/string/class.inc)
(import 'gui/font/class.inc)
(import 'gui/texture/class.inc)
(import 'class/symbol/class.inc)
(import 'class/vector/class.inc)
(import 'class/hash_map/class.inc)
(import 'class/pair/class.inc)
(import 'gui/sdl2.inc)

(gen-new 'font)
(gen-create 'font)
(gen-class 'font)

(def-method 'font 'open)
	;r0 = name c string (pubyte)
	;r1 = font size (points)
	;outputs
	;r0 = 0 if error, else font object (ptr)
	;trashes
	;all

	(ptr 'this 'name 'flat_set)
	(pptr 'iter_begin 'iter_end)
	(uint 'points)
	(int 'same)

	(push-scope)
	(entry 'font 'open {name, points})

	(assign {ptr($fonts)} {flat_set})
	(vpifnot {flat_set})
		(call 'vector 'create {} {flat_set})
		(assign {flat_set} {ptr($fonts)})
	(endif)

	;intern font
	(call 'vector 'get_iters {flat_set} {_, iter_begin, iter_end})
	(loop-while {iter_begin != iter_end})
		(assign {*iter_begin} {this})
		(vpif {this->font_points == points})
			(call 'sys_string 'compare {this->font_name->string_data, name} {same})
			(gotoifnot {same} 'found)
		(endif)
		(assign {iter_begin + ptr_size} {iter_begin})
	(loop-end)
	(call 'font 'create {name, points} {this})
	(vpif {this})
		(call 'vector 'push_back {flat_set, this})
	(vp-label 'found)
		(call 'font 'ref {this})
	(endif)

	(exit 'font 'open {this})
	(pop-scope)
	(return)

;;;;;;;;;;;;;;;;;
; interned fonts
;;;;;;;;;;;;;;;;;

	(vp-align ptr_size)
(vp-label 'fonts)
	(vp-long 0)

(def-func-end)

(def-method 'font 'init)
	;inputs
	;r0 = font object (ptr)
	;r1 = vtable (pptr)
	;r2 = name c string (pubyte)
	;r3 = font size (points)
	;outputs
	;r0 = font object (ptr)
	;r1 = 0 if error, else ok
	;trashes
	;all but r0

	(ptr 'this 'name)
	(union
		'(ptr 'vtable)
		'(ulong 'ok))
	(uint 'points)

	(push-scope)
	(entry 'font 'init {this, vtable, name, points})

	;init parent
	(s-call 'font 'init {this, vtable} {_, ok})
	(vpif {ok})
		;init state
		(assign {0, 0, points} {this->font_cnt, this->font_chars, this->font_points})
		(call 'symbol 'intern_cstr {name} {this->font_name})
		(call 'hash_map 'create (cat {@} (f-path 'symbol 'same) {, 61}) {this->font_word_map})

		;kernel callback for open
		(call 'sys_task 'callback {this, $open_callback})
		(vpifnot {this->font_handle})
			(call 'string 'deref {this->font_name})
			(call 'hash_map 'deref {this->font_word_map})
			(assign {0} {ok})
		(endif)
	(endif)

	(exit 'font 'init {this, ok})
	(pop-scope)
	(return)

(vp-label 'open_callback)
	;inputs
	;r0 = user data

	(ptr 'user 'old_stack)

	;align stack
	(vp-cpy-rr rsp r1)
	(push-scope)
	(vp-and-cr -16 rsp)
	(entry {user, old_stack})

	(sdl-call 'sdl 'ttf_open_font {&user->font_name->string_data, user->font_points} {user->font_handle})
	(vpif {user->font_handle})
		;fill in ascent, descent and height
		(sdl-call 'sdl 'ttf_font_ascent {user->font_handle} {user->font_ascent})
		(sdl-call 'sdl 'ttf_font_descent {user->font_handle} {user->font_descent})
		(sdl-call 'sdl 'ttf_font_height {user->font_handle} {user->font_height})
	(endif)

	(assign {old_stack} '(rsp))
	(pop-scope-syms)
	(return)

(def-func-end)

(def-method 'font 'deinit)
	;inputs
	;r0 = font object (ptr)
	;outputs
	;r0 = font object (ptr)
	;trashes
	;all but r0

	(ptr 'this)

	(push-scope)
	(entry 'font 'deinit {this})

	(call 'vector 'deref_if {this->font_chars})
	(call 'hash_map 'deref {this->font_word_map})
	(call 'string 'deref {this->font_name})
	(call 'sys_task 'callback {this, $close_callback})

	(exit 'font 'deinit {this})
	(pop-scope)
	(s-jump 'font 'deinit '(r0))

(vp-label 'close_callback)
	;inputs
	;r0 = user data

	(def-struct 'local)
		(ptr 'old_stack)
	(def-struct-end)

	;align stack
	(vp-cpy-rr rsp r1)
	(vp-alloc local_size)
	(vp-and-cr -16 rsp)
	(entry '(r0 (rsp local_old_stack)))

	(sdl-call 'sdl 'ttf_close_font '((r0 font_handle)))

	(vp-cpy-ir rsp local_old_stack rsp)
	(vp-ret)

(def-func-end)

(def-method 'font 'ref_word)
	;inputs
	;r0 = font object (ptr)
	;r1 = string object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = texture object (ptr)
	;trashes
	;all but r0

	(def-struct 'local)
		(ptr 'this 'word 'txt 'new_map)
	(def-struct-end)

	(ptr 'this 'word 'txt 'new_map)
	(pptr 'iter 'bucket)

	(push-scope)
	(entry 'font 'ref_word {this, word})

	;look up string in word map
	(call 'hash_map 'find {this->font_word_map, word} {_, iter, bucket})
	(vpifnot {iter})
		;create new entry
		(call 'texture 'create {0, 0, 0} {txt})
		(call 'symbol 'ref {word})
		(call 'pair 'create {word, txt} {iter})
		(call 'vector 'push_back {bucket, iter})
		(assign {this->font_cnt + 1} {this->font_cnt})

		;flush cache ?
		(vpif {this->font_cnt >= font_max_word_cache})
			(call 'texture 'ref {txt})
			(call 'hash_map 'create {this->font_word_map->hash_set_key_callback,
				this->font_word_map->hash_set_num_buckets} {new_map})
			(call 'hash_map 'for_each {this->font_word_map, $flush_callback, &this})
			(call 'hash_map 'deref {this->font_word_map})
			(assign {new_map} {this->font_word_map})
			(call 'texture 'deref {txt})
		(endif)

		;kernel callback for word texture creation
		(call 'sys_task 'callback {&this, $word_callback})
	(else)
		(call 'pair 'get_second {*iter} {_, txt})
	(endif)
	(call 'texture 'ref {txt})

	(exit 'font 'ref_word {this, txt})
	(pop-scope)
	(return)

(vp-label 'word_callback)
	;inputs
	;r0 = user data

	(ptr 'user 'surface 'old_stack)
	(ulong 'handle)

	;align stack
	(vp-cpy-rr rsp r1)
	(push-scope)
	(vp-and-cr -16 rsp)
	(entry {user, old_stack})

	(sdl-call 'sdl 'ttf_render_utf8_blended {user->local_this->font_handle, &user->local_word->string_data, 0xffffff} {surface})
	(vpif {surface})
		;create word texture
		(sdl-call 'sdl 'sdl_create_texture_from_surface (cat {@} (f-path 'gui 'statics) {.gui_statics_renderer, surface}) {handle})
		(vpif {handle})
			;texture blend mode and save details
			(sdl-call 'sdl 'sdl_set_texture_blend_mode {handle, SDL_BLENDMODE_BLEND})
			(assign {handle, surface->sdl_surface_w, surface->sdl_surface_h}
				{user->local_txt->texture_handle, user->local_txt->texture_width, user->local_txt->texture_height})
		(endif)
		(sdl-call 'sdl 'sdl_free_surface {surface})
	(endif)

	(assign {old_stack} '(rsp))
	(pop-scope-syms)
	(return)

(vp-label 'flush_callback)
	;inputs
	;r0 = predicate data (ptr)
	;r1 = element iterator (pptr)
	;outputs
	;r1 = 0 if break, else not

	(vp-cpy-ir r1 0 r1)
	(vp-cpy-ir r1 pair_second r2)
	(vp-cpy-ir-ui r2 obj_count r2)
	(vpif '(r2 != 1))
		(vp-cpy-ir r0 local_new_map r2)
		(call 'obj 'ref '(r1))
		(vp-push r0)
		(call 'hash_set 'get_bucket '(r2 r0) '(_ r0))
		(vp-pop r1)
		(jump 'vector 'push_back '(r0 r1))
	(else)
		(vp-cpy-ir r0 local_this r0)
		(vp-cpy-ir-ui r0 font_cnt r2)
		(vp-sub-cr 1 r2)
		(vp-cpy-ri-i r2 r0 font_cnt)
	(endif)
	(vp-ret)

(def-func-end)

(def-method 'font 'ref_chars)
	;inputs
	;r0 = font object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = char texture vector object (ptr)
	;trashes
	;all but r0

	(ptr 'this 'word 'txt)
	(uint 'code)

	;save inputs
	(push-scope)
	(entry 'font 'ref_chars {this})

	;lazy create char textures
	(vpifnot {this->font_chars})
		(call 'vector 'create nil {this->font_chars})
		(call 'vector 'set_capacity {this->font_chars, 128})
		(assign {33} {code})
		(loop-start)
			(call 'symbol 'intern_cstr {&code} {word})
			(call 'font 'ref_word {this, word} {_, txt})
			(call 'vector 'push_back {this->font_chars, txt})
			(call 'symbol 'deref {word})
			(assign {code + 1} {code})
		(loop-until {code == 127})
	(endif)
	(call 'vector 'ref {this->font_chars})

	(exit 'font 'ref_chars {this, this->font_chars})
	(pop-scope)
	(return)

(def-func-end)
